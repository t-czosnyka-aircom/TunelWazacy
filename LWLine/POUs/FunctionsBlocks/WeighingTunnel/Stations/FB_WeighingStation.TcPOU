<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_WeighingStation" Id="{44fe5b5a-0f0b-4511-b67e-0a0f72747565}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_WeighingStation EXTENDS FB_BaseStation
VAR
	fbWeighingActuator: FB_ActuatorOOP_TC;
	fbPusherActuator: FB_ActuatorOOP_TC;
	fbBottleSensor: FB_Sensor;
	fbBottleDrawerSensor: FB_Sensor;
	bDrawerUnlockRequest:BOOL;
	fbDrawerLock: FB_Lock;
	fbWeighing : FB_WeighingOversampling;
	bWeightCheckOK :BOOL;
	bCameraCheckOK :BOOL;
	stInOuts :ST_WeighingStation_InOuts;
END_VAR
VAR PERSISTENT
	stSettings: ST_WeighingStation_Settings;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//SIM/
stInOuts.stDrawerLock.bClosed := TRUE;
stInOuts.stDrawerLock.bLocked := TRUE;

//Call FB_BaseStation
SUPER^(fbTableControl:=fbTableControl);
iStationId:=201;
sStationName :='Weighing Station';

bRotationInterlock:=fbDrawerLock.prClosedAndLocked AND fbPusherActuator.prInHomePosition AND fbWeighingActuator.prInHomePosition;


fbWeighingActuator(stDigInOut:=stInOuts.stWeighingActuator, bI_Control_Ready:=TRUE, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= fbPusherActuator.prInHomePosition, bI_WorkInterlock:=fbTableControl.prTableInPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=1.0,bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive);
					
fbPusherActuator(stDigInOut:=stInOuts.stPusherActuator, bI_Control_Ready:=TRUE, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= TRUE, 
					bI_WorkInterlock:=fbTableControl.prTableInPosition AND fbWeighingActuator.prInWorkPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=1.0,bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive);
					
fbBottleSensor(bSensorSignal:=stInOuts.bBottleDetectionSensor,bSimulationON:=bSimulationON,
uiSensorOnDelay_MS:=500, uiErrorOnDelay_MS:=1000,uiSensorOffDelay_MS:=500, uiErrorOffDelay_MS:=1000);
		
fbDrawerLock(stInOut:=stInOuts.stDrawerLock,bReleaseManulOpening:=bManualModeActive AND fbTableControl.prTableInPosition);
	
IF fbDrawerLock.prNotClosed THEN
	stSettings.iNOK_BottleCounter := 0;
END_IF
			
CASE iWorkStep OF
	0: //CheckState
		IF  __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.prStateIsNOK THEN
			iWorkStep:=50;
		ELSIF bDrawerUnlockRequest THEN
			iWorkStep:=70;
		ELSE
			iWorkStep:=5;
		END_IF

	5: //HomePosition
		IF fbWeighingActuator.mGoToHome(FALSE) AND fbPusherActuator.mGoToHome(FALSE) AND fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=10;
		ELSIF fbWeighingActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingActuatorError);
		ELSIF fbPusherActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingPusherActuatorError);	
		END_IF
		
	
	10: //Wait for Bottle - Table rotation
		IF stSettings.iNOK_BottleCounter >= 5 THEN
			bDrawerUnlockRequest := TRUE;
		END_IF
		IF __ISVALIDREF(fbTablePlace) THEN
			fbBottleSensor.bRequestedState := NOT fbTablePlace.prStateIsEmpty;
			fbBottleSensor.bCheckState := fbTableControl.prTableInPosition;
			bRotationRequest := fbTablePlace.prStateIsAfterWeighing AND fbBottleSensor.prSensorOnOK OR (fbTablePlace.prStateIsEmpty AND fbBottleSensor.prSensorOffOK);
			IF fbTablePlace.prStateIsBeforeWeighing AND fbBottleSensor.prSensorOnOK THEN
				iWorkStep:=20;
			END_IF
		ELSE
			bRotationRequest := TRUE;
		END_IF;
		IF bManualModeRequest THEN
			iWorkStep:= 80;
		END_IF
			
	20: //Weighing Actuator Work
		IF fbWeighingActuator.mGoToWork(FALSE) AND __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleProcessing() THEN
			iWorkStep:=30;
			bWeightCheckOK :=FALSE;
			bCameraCheckOK :=FALSE;
		END_IF
	
	30: //Weight and Camera Check
		iWorkStep:=40;

		IF stSettings.bWeightCheck AND_THEN fbWeighing.mGetMeasurement(1000) THEN
			IF NOT fbWeighing.prSingleMeasurementError THEN
				bWeightCheckOK := ABS(fbWeighing.prSingleMeasurement - stSettings.rRequiredWeight) <=  stSettings.rWeightTolerance;
			END_IF
		ELSE
			bWeightCheckOK := TRUE;
		END_IF
		
		IF bWeightCheckOK AND bCameraCheckOK THEN
			iWorkStep:=40;
		ELSE
			IF  __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleNOK() THEN
				iWorkStep:=50;
			END_IF
		END_IF
		
	
	40: //ALL OK
		IF fbWeighingActuator.mGoToHome(FALSE) AND __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleAfterWeighing() THEN
			iWorkStep:=0;
		END_IF
	
	50: // NOK - Pusher Forward
		IF stSettings.iNOK_BottleCounter  < stSettings.iMaxNOK_Bottles THEN
			IF fbPusherActuator.mGoToWork(FALSE) THEN
				iWorkStep:=55;
			END_IF
		ELSE
			mSetError(nTable_ErrorList.WeighingNOK_DrawerFull);
		END_IF
		
		
	55: //Wait for bottle to fall
		fbStepTON.PT:=T#1S;
		IF fbStepTON.Q THEN
			iWorkStep:=60;
		END_IF
		
	60: //Pusher Back
		IF fbPusherActuator.mGoToHome(FALSE) THEN
			stSettings.iNOK_BottleCounter := stSettings.iNOK_BottleCounter+1;
			IF __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleEmpty() THEN
				iWorkStep:=0;
			END_IF
		ELSIF fbPusherActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingPusherActuatorError);	
		END_IF
		
	70: //Drawer Unlock
		
		fbDrawerLock.mOpen();
		fbStepTON.PT:=T#3S;
		IF NOT fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=71;
		ELSIF fbStepTON.Q THEN
			mSetError(nTable_ErrorList.WeighingDrawerOpeningError);	
		END_IF
	
	71: //Wait Drawer Opened
		fbDrawerLock.mOpen();
		IF NOT fbDrawerLock.prClosed THEN
			bDrawerUnlockRequest:=FALSE;
			iWorkStep:=72;
		END_IF
		
	72: //Wait Drawer Closed
		IF fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=0;
		END_IF
		
		
	80: //Manual Mode
	IF NOT bManualModeRequest THEN
		iWorkStep:=0;
	END_IF
	
	
	90: //Error

		
	91: //Error Reset
		iWorkStep:=0;
		fbWeighingActuator.mErrorReset();
		fbPusherActuator.mErrorReset();
	
END_CASE


]]></ST>
    </Implementation>
    <LineIds Name="FB_WeighingStation">
      <LineId Id="860" Count="70" />
      <LineId Id="1023" Count="1" />
      <LineId Id="931" Count="3" />
      <LineId Id="941" Count="2" />
      <LineId Id="1043" Count="0" />
      <LineId Id="945" Count="0" />
      <LineId Id="1025" Count="1" />
      <LineId Id="947" Count="0" />
      <LineId Id="1027" Count="2" />
      <LineId Id="1039" Count="0" />
      <LineId Id="1041" Count="1" />
      <LineId Id="1040" Count="0" />
      <LineId Id="1030" Count="0" />
      <LineId Id="1020" Count="0" />
      <LineId Id="948" Count="71" />
      <LineId Id="180" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>