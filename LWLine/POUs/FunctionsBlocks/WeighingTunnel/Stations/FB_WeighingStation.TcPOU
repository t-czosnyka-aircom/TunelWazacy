<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_WeighingStation" Id="{44fe5b5a-0f0b-4511-b67e-0a0f72747565}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_WeighingStation EXTENDS FB_BaseStation
VAR_INPUT
	bReleaseWeighing : BOOL;
END_VAR
VAR
	fbWeighingActuator: FB_ActuatorOOP_TC;
	fbPusherActuator: FB_ActuatorOOP_TC;
	fbBottleSensor: FB_Sensor;
	fbBottleDrawerSensor: FB_Sensor;
	bDrawerUnlockRequest:BOOL;
	fbDrawerLock: FB_Lock;
	fbWeighing : FB_WeighingOversampling;
	fbFormatStr: FB_FormatString;
	nCameraResult,nWeightingResult :(NO_RESULT,OK,NOK);
	stInOuts :ST_WeighingStation_InOuts;
	fbTimeStats:fbTimeTable255;
	sWeightText :STRING;
	rWeightRes: REAL;
	rWeightMin: REAL;
	rWeightMax: REAL;

END_VAR
VAR_OUTPUT
	bWeighingInProgess : BOOL;
END_VAR
VAR PERSISTENT
	stSettings: ST_WeighingStation_Settings;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//SIM/
//stInOuts.stDrawerLock.bClosed := TRUE;
//stInOuts.stDrawerLock.bLocked := TRUE;

//Call FB_BaseStation
SUPER^(fbTableControl:=fbTableControl);
iStationId:=201;
sStationName :='Weighing Station';
stSettings.bSingleBottleOnDrawer :=TRUE;

//Types

IF  __ISVALIDREF(fbTablePlace) THEN
	stSettings.stWeighingNextBottleParams := fbTableControl.mGetBottleWeighingParams(fbTableControl.prNextType);
	stSettings.stWeighingBottleParams := fbTableControl.mGetBottleWeighingParams(fbTablePlace.prBottleType);
END_IF
stSettings.iMaxNOK_Bottles := stSettings.stWeighingNextBottleParams.iMaxNOK_Bottles;
stSettings.iNOK_BottlesSensorDetect := stSettings.stWeighingNextBottleParams.iNOK_BottlesSensorDetect;
IF stSettings.bSingleBottleOnDrawer THEN
	stSettings.iMaxNOK_Bottles:= 1;
END_IF

//Station Empty
bIsEmpty:= NOT fbBottleSensor.bSensorSignal AND stSettings.iNOK_BottleCounter=0;


bRotationInterlock:=fbDrawerLock.prClosedAndLocked AND fbPusherActuator.prInHomePosition AND fbWeighingActuator.prInHomePosition;


fbWeighingActuator(stDigInOut:=stInOuts.stWeighingActuator, bI_Control_Ready:=prSafetyOk, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= fbPusherActuator.prInHomePosition, bI_WorkInterlock:=fbTableControl.prTableInPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=2.0, rI_WorkPositionDelayMS:=500, bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive,
 					bI_RespectInterlockInMAN := TRUE);
					
fbPusherActuator(stDigInOut:=stInOuts.stPusherActuator, bI_Control_Ready:=prSafetyOk, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= TRUE, 
					bI_WorkInterlock:=fbWeighingActuator.prInWorkPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=1.0,bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive,
					bI_RespectInterlockInMAN := TRUE);
					
fbBottleSensor(bSimulationON:=bSimulationON OR bGhostRunOn,
uiSensorOnDelay_MS:=500, uiErrorOnDelay_MS:=1000,uiSensorOffDelay_MS:=500, uiErrorOffDelay_MS:=1000);

fbBottleDrawerSensor(bSimulationON:=bSimulationON,
uiSensorOnDelay_MS:=500, uiErrorOnDelay_MS:=1000,uiSensorOffDelay_MS:=500, uiErrorOffDelay_MS:=1000);
		
fbDrawerLock(stInOut:=stInOuts.stDrawerLock,bReleaseOpening:=fbTableControl.prTableInPosition, bManualMode:=bManualModeActive);
	
IF fbDrawerLock.prNotClosed THEN
	stSettings.iNOK_BottleCounter := 0;
END_IF

//Drawer Sensor
fbBottleDrawerSensor.bCheckState := fbDrawerLock.prClosedAndLocked AND fbTableControl.prTableInPosition;
fbBottleDrawerSensor.bRequestedState := stSettings.iNOK_BottleCounter < stSettings.iNOK_BottlesSensorDetect;
IF fbBottleDrawerSensor.prSensorError AND NOT bError AND NOT bDrawerUnlockRequest THEN
	mSetError(nTable_ErrorList.WeighingNOK_DrawerCountError);
	bDrawerUnlockRequest := TRUE;
END_IF

//Weighing in Progress
bWeighingInProgess := __ISVALIDREF(fbTablePlace) AND_THEN (fbTablePlace.prStateIsBeforeWeighing OR fbTablePlace.prStateIsProcessing);

bRotationRequest:= FALSE;			
CASE iWorkStep OF
	0: //CheckState
		fbTableControl.fbPIM.mSetNewMachineState(nMachineStates._0_Initialization);
		IF bManualModeRequest THEN
			iWorkStep:= 80;
		ELSE
			iWorkStep:=5;
		END_IF

	5: //HomePosition
		fbTableControl.fbPIM.mSetNewMachineState(nMachineStates._0_Standby_NotReady);
		IF fbPusherActuator.mGoToHome(FALSE) AND_THEN fbWeighingActuator.mGoToHome(FALSE) AND  fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=10;
		ELSIF fbWeighingActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingActuatorError);
		ELSIF fbPusherActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingPusherActuatorError);	
		END_IF
		
	
	10: //Wait for Bottle - Table rotation
		fbTableControl.fbPIM.mSetNewMachineState(nMachineStates._2_ReadyForNewOperation);
		IF __ISVALIDREF(fbTablePlace) AND fbTableControl.prWorkPermission THEN
			fbBottleSensor.bRequestedState := NOT fbTablePlace.prStateIsEmpty AND NOT fbTablePlace.prStateIsDrawer AND NOT fbTablePlace.prStateIsTare ;
			fbBottleSensor.bCheckState := fbTableControl.prTableInPosition;
			bRotationRequest :=  (fbTablePlace.prStateIsAfterWeighing AND fbBottleSensor.prSensorOnOK OR (fbTablePlace.prStateIsEmpty AND fbBottleSensor.prSensorOffOK)) AND NOT bDrawerUnlockRequest AND fbBottleDrawerSensor.prSensorOK;
			IF fbTablePlace.prStateIsNOK AND fbBottleSensor.prSensorOnOK THEN
				iWorkStep:=50;
			ELSIF bDrawerUnlockRequest OR fbTablePlace.prStateIsDrawer AND fbBottleSensor.prSensorOK THEN
				iWorkStep:=70;
			ELSIF (fbTablePlace.prStateIsBeforeWeighing OR fbTablePlace.prStateIsProcessing )AND fbBottleSensor.prSensorOnOK AND fbBottleDrawerSensor.prSensorOK 
				 AND_THEN fbTableControl.fbPIM.mSetNewMachineState(nMachineStates._3_NewOperationStarted) THEN
				iWorkStep:=20;
			ELSIF  fbTablePlace.prStateIsTare AND fbBottleSensor.prSensorOK AND fbBottleDrawerSensor.prSensorOK  THEN
				iWorkStep:=76;
			ELSIF fbBottleSensor.prSensorError THEN
				mSetError(nTable_ErrorList.WeighingSensorError);
			END_IF
		END_IF;
		IF bManualModeRequest THEN
			iWorkStep:= 80;
		END_IF

			
	20: //Weighing Actuator Work
		fbTableControl.fbPIM.mSetNewMachineState(nMachineStates._4_OperationInProgress);
		fbWeighingActuator.mGoToWork(FALSE);
		IF fbWeighingActuator.prInWorkPosition AND __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleProcessing() THEN
			iWorkStep:=30;
			nWeightingResult :=NO_RESULT;
			nCameraResult :=NO_RESULT;
			sWeightText := '';
		ELSIF fbWeighingActuator.prGoToWorkError THEN
			mSetError(nTable_ErrorList.WeighingActuatorError);
		END_IF
	
	30: //Weight and Camera Check
		fbStepTON.PT:=T#30S;
		//Weight Check
		IF stSettings.bWeightCheck AND bReleaseWeighing AND_THEN fbWeighing.mGetMeasurement(1000) THEN
			IF NOT fbWeighing.prSingleMeasurementError THEN
				rWeightRes := fbWeighing.prSingleMeasurement;
				rWeightMin := stSettings.stWeighingBottleParams.rTargetWeight-stSettings.stWeighingBottleParams.rWeightTolerance;
				rWeightMax := stSettings.stWeighingBottleParams.rWeightTolerance + stSettings.stWeighingBottleParams.rTargetWeight;
				fbFormatStr(sFormat:='Waga %.2f[g], min %.2f[g], max %.2f[g]', arg1:=F_REAL(rWeightRes), arg2:=F_REAL(rWeightMin), arg3:=F_REAL(rWeightMax) );
				IF ABS(fbWeighing.prSingleMeasurement - stSettings.stWeighingBottleParams.rTargetWeight) <=   stSettings.stWeighingBottleParams.rWeightTolerance THEN
					nWeightingResult :=OK;
					sWeightText:= CONCAT(' Test OK: ',fbFormatStr.sOut);
				ELSIF fbWeighing.prSingleMeasurement > stSettings.stWeighingBottleParams.rTargetWeight + stSettings.stWeighingBottleParams.rWeightTolerance THEN
					fbTablePlace.mDetectionStatus(3); //TOO HIGH
					nWeightingResult := NOK;
					sWeightText:= CONCAT(' Test NOK: ',fbFormatStr.sOut);
				ELSE
					fbTablePlace.mDetectionStatus(2); //TOO LOW
					nWeightingResult := NOK;
					sWeightText:= CONCAT(' Test NOK: ',fbFormatStr.sOut);
				END_IF
			ELSE
				nWeightingResult := NOK;
				sWeightText := ' Blad pomiaru.';
			END_IF	
		ELSIF NOT stSettings.bWeightCheck THEN
			nWeightingResult :=OK;
		END_IF
		
		//Camera Check
		IF stSettings.bCameraCheck THEN
			nCameraResult:=NOK;
		ELSE
			nCameraResult:=OK;
		END_IF
		
		//Results
		IF nWeightingResult = OK  AND nCameraResult = OK THEN
			IF sWeightText <> '' THEN
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, sWeightText));
			END_IF
			fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Test OK'));	
			IF __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleAfterWeighing() AND_THEN  fbTableControl.fbPIM.mSetNewMachineState(nMachineStates._5_OperationEndedOK) THEN
				iWorkStep:=40;
			END_IF
		ELSIF fbStepTON.Q THEN
			IF nWeightingResult=NO_RESULT THEN 
				mSetError(nTable_ErrorList.WeighingTimeout);
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Weight No Result'));
			END_IF
			IF nCameraResult=NO_RESULT THEN 
				mSetError(nTable_ErrorList.WeighingCameraInspectionTimeout);
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Camera No Result'));
			END_IF
		ELSIF  __ISVALIDREF(fbTablePlace) AND (nWeightingResult = NOK OR nCameraResult = NOK) AND_THEN fbTablePlace.mBottleNOK() THEN
			IF nCameraResult = NOK THEN
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Camera NOK'));
			END_IF
			IF nWeightingResult = NOK THEN
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, sWeightText));
			END_IF
			iWorkStep:=50;
		END_IF
		
	
	40: //ALL OK
		fbTableControl.fbPIM.mSetNewMachineState(nMachineStates._6_ReturningToBaseState);
		IF fbWeighingActuator.mGoToHome(FALSE) THEN
			iWorkStep:=0;
		ELSIF fbWeighingActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingActuatorError);
		END_IF
	
	50: // NOK - Pusher Forward
		IF stSettings.iNOK_BottleCounter  < stSettings.iMaxNOK_Bottles THEN
			IF fbWeighingActuator.mGoToWork(FALSE) AND_THEN fbPusherActuator.mGoToWork(FALSE) THEN
				stSettings.iNOK_BottleCounter := stSettings.iNOK_BottleCounter+1;
				IF stSettings.iNOK_BottleCounter >= stSettings.iMaxNOK_Bottles THEN
					bDrawerUnlockRequest := TRUE;
				END_IF
				IF __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleDrawer() THEN
					iWorkStep:=55;
				END_IF
			ELSIF fbWeighingActuator.prGoToWorkError THEN
				mSetError(nTable_ErrorList.WeighingActuatorError);
			ELSIF fbPusherActuator.prGoToWorkError THEN
				mSetError(nTable_ErrorList.WeighingPusherActuatorError);
			END_IF
		ELSE
			mSetError(nTable_ErrorList.WeighingNOK_DrawerFull);
		END_IF
		
		
	55: //Wait for bottle to fall
		fbStepTON.PT:=T#1S;
		IF fbStepTON.Q THEN
			iWorkStep:=60;
		END_IF
		
	60: //Pusher Back
		IF fbPusherActuator.mGoToHome(FALSE) THEN
			iWorkStep:=0;
		ELSIF fbPusherActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingPusherActuatorError);	
		END_IF

	70: //Drawer Unlock
		//ServoDisable
		IF fbTableControl.mRequestServoDisable() THEN
			fbDrawerLock.mOpen();
		END_IF
		IF NOT fbDrawerLock.prClosedAndLocked OR bSimulationON THEN
			iWorkStep:=71;
		ELSIF fbDrawerLock.prOpeningError THEN
			mSetError(nTable_ErrorList.WeighingDrawerOpeningError);	
		END_IF
	
	71: //Wait Drawer Opened
		fbDrawerLock.mOpen();
		IF NOT fbDrawerLock.prClosed OR bSimulationON THEN
			bDrawerUnlockRequest:=FALSE;
			iWorkStep:=72;
			stSettings.iNOK_BottleCounter := 0;
		END_IF
		
	72: //Error assignment
		IF stSettings.bSingleBottleOnDrawer THEN
			IF __ISVALIDREF(fbTablePlace) THEN
				CASE fbTablePlace.stPlace.byDetectionStatus OF
					0:	//Nic
						
					1:	//Wrong C Ring
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.wrongSealDetected ,sMessageString:='',tMessageTime:=T#0S);
					2:	//Lack of C Ring
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.lackOfDetectionOfSeal,sMessageString:='',tMessageTime:=T#0S);
					4:	//Bottle mass to low
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.massTooSmall,sMessageString:='',tMessageTime:=T#0S);
					5:	//Bottle mass to low + Wrong C Ring
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.bottleMassLowAndWrongCRing,sMessageString:='',tMessageTime:=T#0S);
					6:	//Bottle mass to low + Lack of C Ring
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.bottleMassLowAndLackOfCRing,sMessageString:='',tMessageTime:=T#0S);
					8:	//Bottle mass to high
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.massTooHigh,sMessageString:='',tMessageTime:=T#0S);
					9:	//Bottle mass to high + Wrong C Ring
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.bottleMassHighAndWrongCRing,sMessageString:='',tMessageTime:=T#0S);
					10:	//Bottle mass to high + Lack of C Ring
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.bottleMassHighAndLackOfCRing,sMessageString:='',tMessageTime:=T#0S);
					16: //Bottle not detected
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.bottleNoBottleDetected,sMessageString:='',tMessageTime:=T#0S);
					20: //Bottle not tetected + Bottle mass too Low
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.bottleMassLowAndNoBottleDetected,sMessageString:='',tMessageTime:=T#0S);
					24: //Bottle not tetected + Bottle mass too High
						fbTableControl.mNewEvent(nEventClass := nMachineEventType.Error, nEvent:= nTable_ErrorList.bottleMassHighAndNoBottleDetected,sMessageString:='',tMessageTime:=T#0S);
				END_CASE
			END_IF
		END_IF
		IF __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleEmpty() THEN
			iWorkStep:=75;
		END_IF
		
	75: //Wait Drawer Closed
		IF fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=0;
		END_IF
		
	76: //Tare Request - Weighing Actuator go to work
		fbWeighingActuator.mGoToWork(FALSE);
		IF fbWeighingActuator.prInWorkPosition THEN
			iWorkStep:=77;
		ELSIF fbWeighingActuator.prGoToWorkError THEN
			mSetError(nTable_ErrorList.WeighingActuatorError);
		END_IF
		
	77: //Tare
		IF fbWeighing.mTare() THEN
			IF __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleEmpty() THEN
				iWorkStep:=0;
			END_IF
		END_IF
	
		
	80: //Manual Mode
		fbTableControl.fbPIM.mSetNewMachineState(nMachineStates._1_InManualControl);
		IF NOT bManualModeRequest THEN
			iWorkStep:=0;
		END_IF
	
	
	90: //Error

		
	91: //Error Reset
		fbWeighingActuator.mErrorReset();
		fbPusherActuator.mErrorReset();
		mErrorReset();
		iWorkStep:=0;
END_CASE


fbTimeStats.rRowTime:=0;]]></ST>
    </Implementation>
    <LineIds Name="FB_WeighingStation">
      <LineId Id="1740" Count="8" />
      <LineId Id="2315" Count="0" />
      <LineId Id="1749" Count="0" />
      <LineId Id="2283" Count="0" />
      <LineId Id="1750" Count="0" />
      <LineId Id="2284" Count="0" />
      <LineId Id="1751" Count="1" />
      <LineId Id="2285" Count="1" />
      <LineId Id="2318" Count="1" />
      <LineId Id="2317" Count="0" />
      <LineId Id="2316" Count="0" />
      <LineId Id="2079" Count="0" />
      <LineId Id="2078" Count="0" />
      <LineId Id="1753" Count="26" />
      <LineId Id="2022" Count="0" />
      <LineId Id="1984" Count="4" />
      <LineId Id="2106" Count="0" />
      <LineId Id="1989" Count="0" />
      <LineId Id="2021" Count="0" />
      <LineId Id="2016" Count="1" />
      <LineId Id="1780" Count="3" />
      <LineId Id="1785" Count="7" />
      <LineId Id="2240" Count="0" />
      <LineId Id="1793" Count="14" />
      <LineId Id="2210" Count="1" />
      <LineId Id="2207" Count="0" />
      <LineId Id="1809" Count="1" />
      <LineId Id="2268" Count="0" />
      <LineId Id="1811" Count="0" />
      <LineId Id="2455" Count="1" />
      <LineId Id="2212" Count="1" />
      <LineId Id="1812" Count="0" />
      <LineId Id="1815" Count="6" />
      <LineId Id="2269" Count="0" />
      <LineId Id="1822" Count="0" />
      <LineId Id="1824" Count="3" />
      <LineId Id="2049" Count="0" />
      <LineId Id="2278" Count="0" />
      <LineId Id="2277" Count="0" />
      <LineId Id="1828" Count="14" />
      <LineId Id="2398" Count="0" />
      <LineId Id="1843" Count="1" />
      <LineId Id="2394" Count="3" />
      <LineId Id="1845" Count="17" />
      <LineId Id="2050" Count="0" />
      <LineId Id="1863" Count="0" />
      <LineId Id="2051" Count="0" />
      <LineId Id="1864" Count="0" />
      <LineId Id="2271" Count="0" />
      <LineId Id="1865" Count="0" />
      <LineId Id="2272" Count="0" />
      <LineId Id="1866" Count="20" />
      <LineId Id="2270" Count="0" />
      <LineId Id="1887" Count="1" />
      <LineId Id="2274" Count="1" />
      <LineId Id="1889" Count="4" />
      <LineId Id="2144" Count="2" />
      <LineId Id="2143" Count="0" />
      <LineId Id="2426" Count="0" />
      <LineId Id="2138" Count="0" />
      <LineId Id="2427" Count="0" />
      <LineId Id="2141" Count="1" />
      <LineId Id="2139" Count="1" />
      <LineId Id="1896" Count="13" />
      <LineId Id="2330" Count="0" />
      <LineId Id="1917" Count="4" />
      <LineId Id="2523" Count="0" />
      <LineId Id="2521" Count="0" />
      <LineId Id="1922" Count="0" />
      <LineId Id="2522" Count="0" />
      <LineId Id="1924" Count="12" />
      <LineId Id="2334" Count="0" />
      <LineId Id="2332" Count="0" />
      <LineId Id="2335" Count="0" />
      <LineId Id="2387" Count="0" />
      <LineId Id="2344" Count="6" />
      <LineId Id="2353" Count="5" />
      <LineId Id="2361" Count="5" />
      <LineId Id="2376" Count="4" />
      <LineId Id="2336" Count="0" />
      <LineId Id="2382" Count="0" />
      <LineId Id="2388" Count="0" />
      <LineId Id="2391" Count="1" />
      <LineId Id="2333" Count="0" />
      <LineId Id="2393" Count="0" />
      <LineId Id="1937" Count="4" />
      <LineId Id="2457" Count="1" />
      <LineId Id="2469" Count="2" />
      <LineId Id="2475" Count="1" />
      <LineId Id="2460" Count="2" />
      <LineId Id="2488" Count="0" />
      <LineId Id="2491" Count="2" />
      <LineId Id="2490" Count="0" />
      <LineId Id="2466" Count="0" />
      <LineId Id="1943" Count="1" />
      <LineId Id="2241" Count="0" />
      <LineId Id="1945" Count="15" />
      <LineId Id="180" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>