<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_WeighingStation" Id="{44fe5b5a-0f0b-4511-b67e-0a0f72747565}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_WeighingStation EXTENDS FB_BaseStation
VAR
	fbWeighingActuator: FB_ActuatorOOP_TC;
	fbPusherActuator: FB_ActuatorOOP_TC;
	fbBottleSensor: FB_Sensor;
	fbBottleDrawerSensor: FB_Sensor;
	bDrawerUnlockRequest:BOOL;
	fbDrawerLock: FB_Lock;
	fbWeighing : FB_WeighingOversampling;
	
	nCameraResult,nWeightingResult :(NO_RESULT,OK,NOK);
	stInOuts :ST_WeighingStation_InOuts;
	fbTimeStats:fbTimeTable255;
END_VAR
VAR PERSISTENT
	stSettings: ST_WeighingStation_Settings;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//SIM/
//stInOuts.stDrawerLock.bClosed := TRUE;
//stInOuts.stDrawerLock.bLocked := TRUE;

//Call FB_BaseStation
SUPER^(fbTableControl:=fbTableControl);
iStationId:=201;
sStationName :='Weighing Station';

//Types
IF  __ISVALIDREF(fbTablePlace) THEN
	stSettings.stWeighingBottleParams := fbTableControl.mGetBottleWeighingParams(fbTablePlace.prBottleType);
END_IF


bRotationInterlock:=fbDrawerLock.prClosedAndLocked AND fbPusherActuator.prInHomePosition AND fbWeighingActuator.prInHomePosition OR TRUE;


fbWeighingActuator(stDigInOut:=stInOuts.stWeighingActuator, bI_Control_Ready:=TRUE, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= fbPusherActuator.prInHomePosition, bI_WorkInterlock:=fbTableControl.prTableInPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=1.0,bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive);
					
fbPusherActuator(stDigInOut:=stInOuts.stPusherActuator, bI_Control_Ready:=TRUE, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= TRUE, 
					bI_WorkInterlock:=fbTableControl.prTableInPosition AND fbWeighingActuator.prInWorkPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=1.0,bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive);
					
fbBottleSensor(bSensorSignal:=stInOuts.bBottleDetectionSensor,bSimulationON:=bSimulationON,
uiSensorOnDelay_MS:=500, uiErrorOnDelay_MS:=1000,uiSensorOffDelay_MS:=500, uiErrorOffDelay_MS:=1000);
		
fbDrawerLock(stInOut:=stInOuts.stDrawerLock,bReleaseManulOpening:=bManualModeActive AND fbTableControl.prTableInPosition);
	
IF fbDrawerLock.prNotClosed THEN
	stSettings.iNOK_BottleCounter := 0;
END_IF

bRotationRequest:= FALSE;			
CASE iWorkStep OF
	0: //CheckState
		IF  __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.prStateIsNOK THEN
			iWorkStep:=50;
		ELSE
			iWorkStep:=5;
		END_IF

	5: //HomePosition
		IF fbWeighingActuator.mGoToHome(FALSE) AND fbPusherActuator.mGoToHome(FALSE) AND fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=10;
		ELSIF fbWeighingActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingActuatorError);
		ELSIF fbPusherActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingPusherActuatorError);	
		END_IF
		
	
	10: //Wait for Bottle - Table rotation

		IF __ISVALIDREF(fbTablePlace) AND fbTableControl.prWorkPermission THEN
			fbBottleSensor.bRequestedState := NOT fbTablePlace.prStateIsEmpty;
			fbBottleSensor.bCheckState := fbTableControl.prTableInPosition;
			bRotationRequest := fbTablePlace.prStateIsAfterWeighing AND fbBottleSensor.prSensorOnOK OR (fbTablePlace.prStateIsEmpty AND fbBottleSensor.prSensorOffOK AND NOT bDrawerUnlockRequest);
			IF bDrawerUnlockRequest AND (fbTablePlace.prStateIsBeforeWeighing AND fbBottleSensor.prSensorOnOK  OR fbTablePlace.prStateIsEmpty AND fbBottleSensor.prSensorOffOK) THEN
				iWorkStep:=70;
			ELSIF fbTablePlace.prStateIsBeforeWeighing AND fbBottleSensor.prSensorOnOK THEN
				iWorkStep:=20;
			END_IF
//		ELSE
//			bRotationRequest := TRUE;
		END_IF;
		IF bManualModeRequest THEN
			iWorkStep:= 80;
		END_IF
			
	20: //Weighing Actuator Work
		IF fbWeighingActuator.mGoToWork(FALSE) AND __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleProcessing() THEN
			iWorkStep:=30;
			nWeightingResult :=NO_RESULT;
			nCameraResult :=NO_RESULT;
		END_IF
	
	30: //Weight and Camera Check
		fbStepTON.PT:=T#10S;
		//Weight Check
		IF stSettings.bWeightCheck AND_THEN fbWeighing.mGetMeasurement(1000) THEN
			nWeightingResult := NOK;
			IF NOT fbWeighing.prSingleMeasurementError AND ABS(fbWeighing.prSingleMeasurement - stSettings.stWeighingBottleParams.rTargetWeight) <=   stSettings.stWeighingBottleParams.rWeightTolerance THEN
				nWeightingResult :=OK;
			END_IF
		ELSIF NOT stSettings.bWeightCheck THEN
			nWeightingResult :=OK;
		END_IF
		
		//Camera Check
		IF stSettings.bCameraCheck THEN
			nCameraResult:=NOK;
		ELSE
			nCameraResult:=OK;
		END_IF
		
		//Results
		
		IF nWeightingResult =OK  AND nCameraResult =OK THEN
			fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Test OK'));	
			iWorkStep:=40;
		ELSIF fbStepTON.Q THEN
			IF nWeightingResult=NO_RESULT THEN 
				mSetError(nTable_ErrorList.WeighingTimeout);
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Weight No Result'));
			END_IF
			IF nCameraResult=NO_RESULT THEN 
				mSetError(nTable_ErrorList.CameraInspectionTimeout);
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Camera No Result'));
			END_IF
		ELSE
			IF nWeightingResult = NOK THEN
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Weight NOK'));
			END_IF
			IF nCameraResult = NOK THEN
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Camera NOK'));
			END_IF
			IF  __ISVALIDREF(fbTablePlace) AND (nWeightingResult = NOK OR nCameraResult = NOK) AND_THEN fbTablePlace.mBottleNOK() THEN
				iWorkStep:=50;
			END_IF
		END_IF
		
	
	40: //ALL OK
		IF fbWeighingActuator.mGoToHome(FALSE) AND __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleAfterWeighing() THEN
			iWorkStep:=0;
		END_IF
	
	50: // NOK - Pusher Forward
		IF stSettings.iNOK_BottleCounter  < stSettings.iMaxNOK_Bottles THEN
			IF fbPusherActuator.mGoToWork(FALSE) THEN
				iWorkStep:=55;

			END_IF
		ELSE
			mSetError(nTable_ErrorList.WeighingNOK_DrawerFull);
		END_IF
		
		
	55: //Wait for bottle to fall
		fbStepTON.PT:=T#1S;
		IF fbStepTON.Q THEN
			iWorkStep:=60;
		END_IF
		
	60: //Pusher Back
		IF fbPusherActuator.mGoToHome(FALSE) THEN
			stSettings.iNOK_BottleCounter := stSettings.iNOK_BottleCounter+1;
			IF stSettings.iNOK_BottleCounter >= stSettings.iMaxNOK_Bottles THEN
				bDrawerUnlockRequest := TRUE;
			END_IF
			IF __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleEmpty() THEN
				iWorkStep:=0;
			END_IF
		ELSIF fbPusherActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingPusherActuatorError);	
		END_IF
		
	70: //Drawer Unlock
		fbDrawerLock.mOpen();
		fbStepTON.PT:=T#3S;
		IF NOT fbDrawerLock.prClosedAndLocked OR bSimulationON THEN
			iWorkStep:=71;
		ELSIF fbStepTON.Q THEN
			mSetError(nTable_ErrorList.WeighingDrawerOpeningError);	
		END_IF
	
	71: //Wait Drawer Opened
		fbDrawerLock.mOpen();
		IF NOT fbDrawerLock.prClosed OR bSimulationON THEN
			bDrawerUnlockRequest:=FALSE;
			iWorkStep:=72;
			stSettings.iNOK_BottleCounter := 0;
		END_IF
		
	72: //Wait Drawer Closed
		IF fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=0;
		END_IF
		
		
	80: //Manual Mode
	IF NOT bManualModeRequest THEN
		iWorkStep:=0;
	END_IF
	
	
	90: //Error

		
	91: //Error Reset
		fbWeighingActuator.mErrorReset();
		fbPusherActuator.mErrorReset();
		mErrorReset();
		iWorkStep:=0;
END_CASE


fbTimeStats.rRowTime:=0;]]></ST>
    </Implementation>
    <LineIds Name="FB_WeighingStation">
      <LineId Id="1344" Count="7" />
      <LineId Id="1595" Count="2" />
      <LineId Id="1618" Count="0" />
      <LineId Id="1599" Count="0" />
      <LineId Id="1594" Count="0" />
      <LineId Id="1352" Count="26" />
      <LineId Id="1381" Count="14" />
      <LineId Id="1398" Count="5" />
      <LineId Id="1577" Count="0" />
      <LineId Id="1576" Count="0" />
      <LineId Id="1573" Count="0" />
      <LineId Id="1405" Count="39" />
      <LineId Id="1534" Count="0" />
      <LineId Id="1445" Count="1" />
      <LineId Id="1535" Count="0" />
      <LineId Id="1537" Count="2" />
      <LineId Id="1536" Count="0" />
      <LineId Id="1450" Count="21" />
      <LineId Id="1565" Count="0" />
      <LineId Id="1472" Count="14" />
      <LineId Id="1570" Count="1" />
      <LineId Id="1569" Count="0" />
      <LineId Id="1487" Count="7" />
      <LineId Id="1496" Count="12" />
      <LineId Id="1564" Count="0" />
      <LineId Id="1509" Count="17" />
      <LineId Id="1528" Count="2" />
      <LineId Id="1568" Count="0" />
      <LineId Id="1531" Count="2" />
      <LineId Id="180" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>