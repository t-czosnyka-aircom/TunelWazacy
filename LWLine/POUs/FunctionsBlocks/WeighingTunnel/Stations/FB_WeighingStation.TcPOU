<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_WeighingStation" Id="{44fe5b5a-0f0b-4511-b67e-0a0f72747565}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_WeighingStation EXTENDS FB_BaseStation
VAR_INPUT
	bReleaseWeighing : BOOL;
END_VAR
VAR
	fbWeighingActuator: FB_ActuatorOOP_TC;
	fbPusherActuator: FB_ActuatorOOP_TC;
	fbBottleSensor: FB_Sensor;
	fbBottleDrawerSensor: FB_Sensor;
	bDrawerUnlockRequest:BOOL;
	fbDrawerLock: FB_Lock;
	fbWeighing : FB_WeighingOversampling;
	fbFormatStr: FB_FormatString;
	nCameraResult,nWeightingResult :(NO_RESULT,OK,NOK);
	stInOuts :ST_WeighingStation_InOuts;
	fbTimeStats:fbTimeTable255;
	sWeightText :STRING;
	rWeightRes: REAL;
	rWeightMin: REAL;
	rWeightMax: REAL;
END_VAR
VAR_OUTPUT
	bWeighingInProgess : BOOL;
END_VAR
VAR PERSISTENT
	stSettings: ST_WeighingStation_Settings;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//SIM/
//stInOuts.stDrawerLock.bClosed := TRUE;
//stInOuts.stDrawerLock.bLocked := TRUE;

//Call FB_BaseStation
SUPER^(fbTableControl:=fbTableControl);
iStationId:=201;
sStationName :='Weighing Station';

//Types
IF  __ISVALIDREF(fbTablePlace) THEN
	stSettings.stWeighingBottleParams := fbTableControl.mGetBottleWeighingParams(fbTablePlace.prBottleType);
END_IF


bRotationInterlock:=fbDrawerLock.prClosedAndLocked AND fbPusherActuator.prInHomePosition AND fbWeighingActuator.prInHomePosition OR TRUE;


fbWeighingActuator(stDigInOut:=stInOuts.stWeighingActuator, bI_Control_Ready:=TRUE, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= fbPusherActuator.prInHomePosition, bI_WorkInterlock:=fbTableControl.prTableInPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=1.0,bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive,
 					bI_RespectInterlockInMAN := FALSE);
					
fbPusherActuator(stDigInOut:=stInOuts.stPusherActuator, bI_Control_Ready:=TRUE, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= TRUE, 
					bI_WorkInterlock:=fbTableControl.prTableInPosition AND fbWeighingActuator.prInWorkPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=1.0,bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive,
					bI_RespectInterlockInMAN := FALSE);
					
fbBottleSensor(bSensorSignal:=stInOuts.bBottleDetectionSensor,bSimulationON:=bSimulationON OR bGhostRunOn,
uiSensorOnDelay_MS:=500, uiErrorOnDelay_MS:=1000,uiSensorOffDelay_MS:=500, uiErrorOffDelay_MS:=1000);

fbBottleDrawerSensor(bSensorSignal:=stInOuts.bBottleDrawerSensor,bSimulationON:=bSimulationON,
uiSensorOnDelay_MS:=500, uiErrorOnDelay_MS:=1000,uiSensorOffDelay_MS:=500, uiErrorOffDelay_MS:=1000);
		
fbDrawerLock(stInOut:=stInOuts.stDrawerLock,bReleaseOpening:=fbTableControl.prTableInPosition, bManualMode:=bManualModeActive);
	
IF fbDrawerLock.prNotClosed THEN
	stSettings.iNOK_BottleCounter := 0;
END_IF

//Drawer Sensor
fbBottleDrawerSensor.bCheckState := fbDrawerLock.prClosedAndLocked AND fbTableControl.prTableInPosition AND fbPusherActuator.prInHomePosition;
fbBottleDrawerSensor.bRequestedState := stSettings.iNOK_BottleCounter <= 3;
IF fbBottleDrawerSensor.prSensorOffError AND NOT bError THEN
	mSetError(nTable_ErrorList.WeighingNOK_DrawerCountError);
END_IF

//Weighing in Progress
bWeighingInProgess := __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.prStateIsBeforeWeighing;

bRotationRequest:= FALSE;			
CASE iWorkStep OF
	0: //CheckState
		IF  __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.prStateIsNOK THEN
			iWorkStep:=50;
		ELSIF bManualModeRequest THEN
			iWorkStep:= 80;
		ELSE
			iWorkStep:=5;
		END_IF

	5: //HomePosition
		IF fbWeighingActuator.mGoToHome(FALSE) AND fbPusherActuator.mGoToHome(FALSE) AND fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=10;
		ELSIF fbWeighingActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingActuatorError);
		ELSIF fbPusherActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingPusherActuatorError);	
		END_IF
		
	
	10: //Wait for Bottle - Table rotation

		IF __ISVALIDREF(fbTablePlace) AND fbTableControl.prWorkPermission  THEN
			fbBottleSensor.bRequestedState := NOT fbTablePlace.prStateIsEmpty;
			fbBottleSensor.bCheckState := fbTableControl.prTableInPosition;
			bRotationRequest := fbTablePlace.prStateIsAfterWeighing AND fbBottleSensor.prSensorOnOK OR (fbTablePlace.prStateIsEmpty AND fbBottleSensor.prSensorOffOK AND NOT bDrawerUnlockRequest);
			IF bDrawerUnlockRequest AND (fbTablePlace.prStateIsBeforeWeighing AND fbBottleSensor.prSensorOnOK  OR fbTablePlace.prStateIsEmpty AND fbBottleSensor.prSensorOffOK) THEN
				iWorkStep:=70;
			ELSIF fbTablePlace.prStateIsBeforeWeighing OR fbTablePlace.prStateIsProcessing AND fbBottleSensor.prSensorOnOK AND FALSE THEN
				iWorkStep:=20;
			END_IF
//		ELSE
//			bRotationRequest := TRUE;
		END_IF;
		IF bManualModeRequest THEN
			iWorkStep:= 80;
		END_IF

			
	20: //Weighing Actuator Work
		fbWeighingActuator.mGoToWork(FALSE);
		fbStepTON(IN:=fbWeighingActuator.prInWorkPosition, PT:=T#2S); 
		IF fbStepTON.Q AND __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleProcessing() THEN
			iWorkStep:=30;
			nWeightingResult :=NO_RESULT;
			nCameraResult :=NO_RESULT;
			sWeightText := '';
		END_IF
	
	30: //Weight and Camera Check
		fbStepTON.PT:=T#10S;
		//Weight Check
		IF stSettings.bWeightCheck AND bReleaseWeighing AND_THEN fbWeighing.mGetMeasurement(1000) THEN
			IF NOT fbWeighing.prSingleMeasurementError THEN
				rWeightRes := fbWeighing.prSingleMeasurement;
				rWeightMin := stSettings.stWeighingBottleParams.rTargetWeight-stSettings.stWeighingBottleParams.rWeightTolerance;
				rWeightMax := stSettings.stWeighingBottleParams.rWeightTolerance + stSettings.stWeighingBottleParams.rTargetWeight;
				fbFormatStr(sFormat:='Waga %.2f[g], min %.2f[g], max %.2f[g]', arg1:=F_REAL(rWeightRes), arg2:=F_REAL(rWeightMin), arg3:=F_REAL(rWeightMax) );
				IF ABS(fbWeighing.prSingleMeasurement - stSettings.stWeighingBottleParams.rTargetWeight) <=   stSettings.stWeighingBottleParams.rWeightTolerance THEN
					nWeightingResult :=OK;
					sWeightText:= CONCAT(' Test OK: ',fbFormatStr.sOut);
				ELSE
					nWeightingResult := NOK;
					sWeightText:= CONCAT(' Test NOK: ',fbFormatStr.sOut);
				END_IF
			ELSE
				nWeightingResult := NOK;
				sWeightText := ' Blad pomiaru.';
			END_IF	
		ELSIF NOT stSettings.bWeightCheck THEN
			nWeightingResult :=OK;
		END_IF
		
		//Camera Check
		IF stSettings.bCameraCheck THEN
			nCameraResult:=NOK;
		ELSE
			nCameraResult:=OK;
		END_IF
		
		//Results
		IF nWeightingResult = OK  AND nCameraResult = OK THEN
			IF sWeightText <> '' THEN
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, sWeightText));
			END_IF
			fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Test OK'));	
			iWorkStep:=40;
		ELSIF fbStepTON.Q THEN
			IF nWeightingResult=NO_RESULT THEN 
				mSetError(nTable_ErrorList.WeighingTimeout);
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Weight No Result'));
			END_IF
			IF nCameraResult=NO_RESULT THEN 
				mSetError(nTable_ErrorList.CameraInspectionTimeout);
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Camera No Result'));
			END_IF
		ELSIF  __ISVALIDREF(fbTablePlace) AND (nWeightingResult = NOK OR nCameraResult = NOK) AND_THEN fbTablePlace.mBottleNOK() THEN
			IF nCameraResult = NOK THEN
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, ' Camera NOK'));
			END_IF
			IF nWeightingResult = NOK THEN
				fbTimeStats.mPushText(sText:= CONCAT(GVL_Aircom.stAC_DT_Data.sTimeString, sWeightText));
			END_IF
			iWorkStep:=50;
		END_IF
		
	
	40: //ALL OK
		IF fbWeighingActuator.mGoToHome(FALSE) AND __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleAfterWeighing() THEN
			iWorkStep:=0;
		END_IF
	
	50: // NOK - Pusher Forward
		IF stSettings.iNOK_BottleCounter  < stSettings.iMaxNOK_Bottles THEN
			IF fbPusherActuator.mGoToWork(FALSE) THEN
				iWorkStep:=55;

			END_IF
		ELSE
			mSetError(nTable_ErrorList.WeighingNOK_DrawerFull);
		END_IF
		
		
	55: //Wait for bottle to fall
		fbStepTON.PT:=T#1S;
		IF fbStepTON.Q THEN
			iWorkStep:=60;
		END_IF
		
	60: //Pusher Back
		IF fbPusherActuator.mGoToHome(FALSE) THEN
			stSettings.iNOK_BottleCounter := stSettings.iNOK_BottleCounter+1;
			IF stSettings.iNOK_BottleCounter >= stSettings.iMaxNOK_Bottles THEN
				bDrawerUnlockRequest := TRUE;
			END_IF
			IF __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleEmpty() THEN
				iWorkStep:=0;
			END_IF
		ELSIF fbPusherActuator.prGoToHomeError THEN
			mSetError(nTable_ErrorList.WeighingPusherActuatorError);	
		END_IF
		
	70: //Drawer Unlock
		fbDrawerLock.mOpen();
		IF NOT fbDrawerLock.prClosedAndLocked OR bSimulationON THEN
			iWorkStep:=71;
		ELSIF fbDrawerLock.prOpeningError THEN
			mSetError(nTable_ErrorList.WeighingDrawerOpeningError);	
		END_IF
	
	71: //Wait Drawer Opened
		fbDrawerLock.mOpen();
		IF NOT fbDrawerLock.prClosed OR bSimulationON THEN
			bDrawerUnlockRequest:=FALSE;
			iWorkStep:=72;
			stSettings.iNOK_BottleCounter := 0;
		END_IF
		
	72: //Wait Drawer Closed
		IF fbDrawerLock.prClosedAndLocked THEN
			iWorkStep:=0;
		END_IF

		
	80: //Manual Mode
	IF NOT bManualModeRequest THEN
		iWorkStep:=0;
	END_IF
	
	
	90: //Error

		
	91: //Error Reset
		fbWeighingActuator.mErrorReset();
		fbPusherActuator.mErrorReset();
		mErrorReset();
		iWorkStep:=0;
END_CASE


fbTimeStats.rRowTime:=0;]]></ST>
    </Implementation>
    <LineIds Name="FB_WeighingStation">
      <LineId Id="1740" Count="39" />
      <LineId Id="2022" Count="0" />
      <LineId Id="1984" Count="5" />
      <LineId Id="2021" Count="0" />
      <LineId Id="2016" Count="1" />
      <LineId Id="1780" Count="47" />
      <LineId Id="2049" Count="0" />
      <LineId Id="1828" Count="34" />
      <LineId Id="2050" Count="0" />
      <LineId Id="1863" Count="0" />
      <LineId Id="2051" Count="0" />
      <LineId Id="1864" Count="58" />
      <LineId Id="1924" Count="17" />
      <LineId Id="1982" Count="0" />
      <LineId Id="1943" Count="17" />
      <LineId Id="180" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>