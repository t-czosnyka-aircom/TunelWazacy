<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_UnloadingStation" Id="{296f6eb6-2953-417c-abf3-86065ed0eed9}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_UnloadingStation EXTENDS FB_BaseStation
VAR_INPUT
	bWeighingInProgess : BOOL;
END_VAR
VAR
	fbUnloadingActuator: FB_ActuatorOOP_TC;
	fbUnloadingSensor: FB_Sensor;
	stInOuts :ST_UnloadingStation_InOuts;
	fbConveyor : FB_Conveyor;
	bBottleEjected:BOOL;
END_VAR
VAR_OUTPUT
	bReleaseWeighing : BOOL;
END_VAR
VAR PERSISTENT
	stSettings: ST_UnloadingStation_Settings;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[//Call FB_BaseStation
SUPER^(fbTableControl:=fbTableControl);
iStationId:=202;
sStationName :='Unloading Station';

//Types
IF  __ISVALIDREF(fbTablePlace) THEN
	stSettings.stUnloadingBottleParams := fbTableControl.mGetBottleUnloadingParams(fbTablePlace.prBottleType);
END_IF


stInOuts.stUnloadingActuator.bI_WorkPosition := NOT stInOuts.stUnloadingActuator.bI_HomePosition AND stInOuts.stUnloadingActuator.bQ_Work;
bRotationInterlock:=fbUnloadingActuator.prInHomePosition;
fbUnloadingActuator(stDigInOut:=stInOuts.stUnloadingActuator, bI_Control_Ready:=prSafetyOk, bManualMode:=bManualModeActive, 
					bI_HomeInterlock:= TRUE, bI_WorkInterlock:=fbTableControl.prTableInPosition,
					rI_HomeTime_S:=1.0,  rI_WorkTime_S:=1.0,bSimulationOn:=bSimulationON, bManualMode:= bManualModeActive, bI_RespectInterlockInMAN := FALSE);

fbUnloadingSensor(bSimulationON:=bSimulationON OR bGhostRunOn,uiSensorOnDelay_MS:=0);									
	
fbConveyor(bManualMode:=bManualModeActive, bEN_FeedbackError:=FALSE, bMoveRelease:=prSafetyOk);
	
bRotationRequest:= FALSE;	
bReleaseWeighing := fbUnloadingActuator.prInHomePosition;
bBottleEjected S= __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.prStateIsAfterWeighing AND fbUnloadingSensor.prSensorSignal_FTRIG AND iWorkStep >=10 AND iWorkStep <= 30;
CASE iWorkStep OF
	0: //Initialisation
		bBottleEjected:=FALSE;
		IF bManualModeRequest THEN
			iWorkStep:= 80;
		ELSIF fbUnloadingActuator.mGoToHome(FALSE) THEN
			iWorkStep := 10;
		ELSIF fbUnloadingActuator.prGoToHomeError THEN 
			mSetError(nTable_ErrorList.UnloadingActuatorBackwardError);
		END_IF
	
	10: //Wait for bottle - Table Rotation
		IF __ISVALIDREF(fbTablePlace) AND fbTableControl.prWorkPermission THEN
			bRotationRequest := fbTablePlace.prStateIsEmpty;
			IF fbTableControl.prWorkPermission AND fbTablePlace.prStateIsAfterWeighing AND NOT bWeighingInProgess THEN
				iWorkStep := 20;
			END_IF;
		END_IF
		IF bManualModeRequest THEN
			iWorkStep:= 80;
		END_IF

		
	20:	//Eject Bottle
		fbConveyor.bMoveRequest:=TRUE;
		IF fbUnloadingActuator.mGoToWork(FALSE) THEN
			iWorkStep:=30;
		ELSIF fbUnloadingActuator.prGoToWorkError THEN 
			mSetError(nTable_ErrorList.UnloadingActuatorForwardError);
		END_IF
		
	
	30: //Wait for sensor OFF
		fbStepTON(IN:=TRUE, PT:=T#10S);
		IF bBottleEjected THEN
			IF __ISVALIDREF(fbTablePlace) AND_THEN fbTablePlace.mBottleEmpty() THEN
				iWorkStep := 0;
				fbStepTON(IN:=FALSE);
			END_IF
		ELSIF fbStepTON.Q THEN
			mSetError(nTable_ErrorList.UnloadingBottleDetectionError);
			fbStepTON(IN:=FALSE);
		END_IF
	
	80: //Manual Mode
		IF NOT bManualModeRequest THEN
			iWorkStep:=0;
		END_IF
		
	90: //Error 


	91: //Error Reset
		iWorkStep := 0;
		fbUnloadingActuator.mErrorReset();
	
	
END_CASE]]></ST>
    </Implementation>
    <LineIds Name="FB_UnloadingStation">
      <LineId Id="213" Count="9" />
      <LineId Id="371" Count="0" />
      <LineId Id="223" Count="10" />
      <LineId Id="349" Count="0" />
      <LineId Id="367" Count="0" />
      <LineId Id="234" Count="1" />
      <LineId Id="366" Count="0" />
      <LineId Id="320" Count="1" />
      <LineId Id="306" Count="3" />
      <LineId Id="240" Count="7" />
      <LineId Id="250" Count="0" />
      <LineId Id="332" Count="1" />
      <LineId Id="331" Count="0" />
      <LineId Id="253" Count="9" />
      <LineId Id="272" Count="1" />
      <LineId Id="275" Count="3" />
      <LineId Id="369" Count="0" />
      <LineId Id="279" Count="2" />
      <LineId Id="368" Count="0" />
      <LineId Id="282" Count="14" />
      <LineId Id="28" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>