<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_TableControl" Id="{0a8fadd5-a89c-41a7-9596-51b884bfe728}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TableControl EXTENDS fbMachine IMPLEMENTS I_Machine
VAR_INPUT
	arrStations : ARRAY[0..TOTAL_TABLE_PLACES] OF I_WorkingStation;
END_VAR
VAR_IN_OUT
	fbTableAxis:	FB_ServoAxis;
END_VAR
VAR_OUTPUT

END_VAR
VAR	
	bSafetyOk: BOOL;
	stInOuts: ST_TableControl_InOuts;
	bServoError:BOOL;
	bServoNotReady:BOOL;
	arrPlaces : ARRAY[0..TOTAL_TABLE_PLACES] OF FB_TablePlace;
	arrPlacePositions : ARRAY[0..TOTAL_TABLE_PLACES] OF POINTER TO FB_TablePlace;
	i : USINT;
	//I_PimSystem			: I_PIM;
	bRotationRequest: BOOL;
	bRotationInterlock: BOOL;
	bStationsCleared: BOOL;
	bStationsAssigned: BOOL;
	iCurrentPositionNumberSave:INT;
	iLastValidPosition: USINT:=0;
	iNextPos:USINT;
	bWorkPermission:BOOL;
	bStationError:BOOL;
	udiStationErrorNumber:UDINT;
	fbStepTON:TON;
	fbRotationRequestTON:TON;
	fbGoToValidPositionTON:TON;
	iLastStep: INT;
	bError: BOOL;
	nError: nTable_ErrorList;
	nLastError: nTable_ErrorList;

	iCounter :INT;
	bAllEmpty:BOOL;
	bMemOk 	 :BOOL; //Sprawdzenie czy pamiec sie nie zinicjalizowala
	nLampControl:(NONE,RED,WHITE,BLUE,YELLOW,GREEN,RED_BLINK,WHITE_BLINK,YELLOW_BLINK,BLUE_BLINK,GREEN_BLINK);
	bBlinkBit:BOOL;
	fbTONBlinkTimer:TON;
END_VAR

VAR CONSTANT
	TOTAL_TABLE_PLACES:USINT:=8;
	BOTTLE_TYPES_NUMBER: USINT:=10;
END_VAR

VAR PERSISTENT

	stHMI : ST_TableControl_HMI;
	arrTypes : ARRAY[0..BOTTLE_TYPES_NUMBER] OF ST_BottleTypeParams;
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[SUPER^.ACT_PIM_BACKGROUND();
//SIM
IF stInOuts.bQ_SafetyErrorAck THEN
	mErrorReset();
END_IF
fbPIM.stStation.bPimActive := FALSE;
fbPIM.stStation.bTestMode := TRUE;
/////
bWorkPermission := fbPIM.prWorkPermission;
stInOuts.bQ_SafetyRun := TRUE;
stInOuts.bQ_SafetyErrorAck:= stInOuts.bI_SafetyReset_1 OR stInOuts.bI_SafetyReset_2;

fbTableAxis.bSafetyReset := stInOuts.bQ_SafetyErrorAck;
fbTableAxis.bEmergencyStop := stInOuts.bI_SafetyEstopOK AND stInOuts.bI_SafetyGateOK;
bSafetyOk := stInOuts.bI_SafetyEstopOK AND stInOuts.bI_SafetyGateOK;


bAllEmpty:= TRUE;
bMemOk := TRUE;
bStationsAssigned := TRUE;
bRotationRequest := TRUE;
bRotationInterlock := TRUE;
bStationsCleared := TRUE;
bStationError := FALSE;
udiStationErrorNumber := 0;

IF fbTableAxis.prPositionValid THEN
	iLastValidPosition:=prTablePositionNumber;
ELSIF NOT fbTableAxis.prIsHomed THEN
	iLastValidPosition:=1;
END_IF

FOR i:=1 TO TOTAL_TABLE_PLACES DO
	//Places
	arrPlaces[i]();
	bMemOk := bMemOk AND arrPlaces[i].bMemOk;
	arrPlaces[i].prPlaceNumber:=i;
	arrPlaces[i].prCurrentPosition:=((arrPlaces[i].stPlace.iPlaceNumber+iLastValidPosition-2) MOD TOTAL_TABLE_PLACES +1);
	arrPlacePositions[arrPlaces[i].prCurrentPosition]:=ADR(arrPlaces[i]);
	//bRotationRequest := bRotationRequest AND arrPlaces[i].prTableRotationRequest;
	bStationsCleared := bStationsCleared AND arrPlaces[i].pWorkingStation=0;
	arrPlaces[i].prTableInPosition:=fbTableAxis.prPositionValid;
	bAllEmpty := bAllEmpty AND arrPlaces[i].prStateIsEmpty;
	//Stations
	IF arrStations[i] <> 0 THEN
		bStationsCleared := bStationsCleared AND NOT __ISVALIDREF(arrStations[i].prTablePlace);
		bStationsAssigned := bStationsAssigned  AND __ISVALIDREF(arrStations[i].prTablePlace);
		bAllEmpty := bAllEmpty AND arrStations[i].prIsEmpty;
		arrStations[i].prSafetyOk := bSafetyOk;
		IF arrStations[i].prError THEN
			bStationError := TRUE;
			udiStationErrorNumber := arrStations[i].prErrorNumber;
		END_IF
		bRotationInterlock := bRotationInterlock AND arrStations[i].prTableRotationInterlock;
		bRotationRequest := bRotationRequest AND arrStations[i].prTableRotationRequest;
	END_IF
END_FOR


fbTableAxis.prMovementExternalInterlock := bRotationInterlock;


//Assign station pointers
IF fbTableAxis.prPositionValid  AND (bStationsCleared OR NOT bStationsAssigned) THEN
	FOR i:=1 TO TOTAL_TABLE_PLACES DO
		IF arrStations[i] <> 0 THEN
			arrPlacePositions[i]^.pWorkingStation := arrStations[i];
			arrStations[i].prTablePlace REF= arrPlacePositions[i]^;
		END_IF
	END_FOR	
END_IF

//Clear station pointers
IF NOT bStationsCleared AND fbTableAxis.prIsMoving OR iCurrentPositionNumberSave <> prTablePositionNumber THEN 
	FOR i:=1 TO TOTAL_TABLE_PLACES DO
		arrPlaces[i].mClearWorkingStation();
		IF arrStations[i] <> 0 THEN
			arrStations[i].prTablePlace REF= 0;
		END_IF
	END_FOR
END_IF;

//Safety Error
IF  NOT bSafetyOk AND NOT bError THEN
	mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.NoSafetyRelease,sMessageString:='',tMessageTime:=T#0S);
END_IF

//Empty Mode
FOR i:=1 TO TOTAL_TABLE_PLACES DO
	IF arrStations[i] <> 0 THEN
		arrStations[i].prEmptyMode:=stHMI.bEmptyMode AND NOT bAllEmpty;
	END_IF
END_FOR	

//Step Timer		
IF iLastStep <> iWorkStep THEN
	fbStepTON(IN:=FALSE,PT:=T#0S);
END_IF
fbStepTON(IN:=fbStepTON.PT >T#0S);
iLastStep := iWorkStep;

CASE iWorkStep OF 
	0: //Initialization
	
		iWorkStep := 10;


	10: //Wait for rotation request
		fbRotationRequestTON(IN:= bRotationRequest AND bRotationInterlock AND fbTableAxis.prIsReady AND fbTableAxis.prIsHomed AND bWorkPermission,PT:=T#300MS);
		fbGoToValidPositionTON(IN:= bRotationInterlock AND fbTableAxis.prIsReady AND fbTableAxis.prIsHomed AND fbTableAxis.prIsNotMoving AND NOT fbTableAxis.prPositionValid, PT:=T#3S);
		IF fbRotationRequestTON.Q THEN
			fbRotationRequestTON(IN:=FALSE);
			iWorkStep := 30;
			iNextPos:= (iLastValidPosition+1) MOD 9;
		ELSIF fbGoToValidPositionTON.Q THEN //Position Lost - go to last position
			iWorkStep := 30;
			iNextPos:= iLastValidPosition;
		ELSIF stHMI.bEN_AutomaticHoming AND bMemOk AND bAllEmpty AND bRotationInterlock AND fbTableAxis.prIsReady AND NOT fbTableAxis.prIsHomed AND bWorkPermission THEN //Automatic Homing
			iWorkStep := 50;
		END_IF
		IF bStationError  THEN
			mNewEvent(nMachineEventType.Alarm,DINT_TO_INT(udiStationErrorNumber),sMessageString:='',tMessageTime:=T#0S);
		ELSIF fbTableAxis.prIsErrorActive THEN
			mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.ServoError,sMessageString:='',tMessageTime:=T#0S);
		END_IF		
		
	30: //Table rotation
		
		IF fbTableAxis.prIsReady AND fbTableAxis.prIsHomed AND_THEN fbTableAxis.mMoveModuloToPosition(iNextPos,bFast:=TRUE,bError=>bServoError,bNotReady=>bServoNotReady) THEN
			IF bServoError THEN
				mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.ServoError,sMessageString:='',tMessageTime:=T#0S);
			ELSIF bServoNotReady THEN
				mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.ServoNotReady,sMessageString:='',tMessageTime:=T#0S);
			ELSE
				iWorkStep:=40;
			END_IF
		ELSIF fbTableAxis.prIsErrorActive THEN
			mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.ServoError,sMessageString:='',tMessageTime:=T#0S);
		END_IF

	40: // Wait for station Assigned
		
		fbStepTON.PT:=T#5S;
		IF prTableInPosition AND bStationsAssigned THEN
			iCounter := iCounter+1;
			iWorkStep := 0;
		ELSIF fbStepTON.Q THEN
			mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.TablePositionError,sMessageString:='',tMessageTime:=T#0S);
		END_IF
		
	50: // Request Automatic Homing
		IF fbTableAxis.prIsReady AND_THEN fbTableAxis.mFullHoming()  THEN
			IF bServoError THEN
				mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.ServoError,sMessageString:='',tMessageTime:=T#0S);
			ELSIF bServoNotReady THEN
				mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.ServoNotReady,sMessageString:='',tMessageTime:=T#0S);
			ELSE
				iWorkStep:=0;
			END_IF
		ELSIF fbTableAxis.prIsErrorActive THEN
			mNewEvent(nMachineEventType.Alarm,nTable_ErrorList.ServoError,sMessageString:='',tMessageTime:=T#0S);
		END_IF
	
		
	
	90: //Error
		bError := TRUE;
		fbPIM.mSetNewMachineState(nMachineStates._8_WaitForReset);
		iWorkStep:=91;
	
	91: //Wait for error reset
		IF fbPIM.mSetNewMachineState(nMachineStates._9_ResetInProgress) THEN
			iWorkStep:=95;
		END_IF
	
	95: //Error reset
		bError := FALSE;
		nError := 	nTable_ErrorList.NoError;
		IF NOT fbPIM.stStation.bPimActive THEN
			fbPIM.mSetNewMachineState(nMachineStates._10_ServiceReset);
		END_IF
		FOR i:=1 TO TOTAL_TABLE_PLACES DO
			IF arrStations[i] <> 0 AND_THEN arrStations[i].prError THEN
				arrStations[i].mErrorReset();
			END_IF
		END_FOR
		IF fbTableAxis.prIsErrorActive THEN
			fbTableAxis.mErrorReset();
		ELSE
			iWorkStep := 0;
		END_IF;

END_CASE

iCurrentPositionNumberSave := prTablePositionNumber;

//Types
IF stHMI.nCurrentTypeToProduce = nBottleTypes.none THEN
	stHMI.nCurrentTypeToProduce:= nBottleTypes.first;
END_IF
IF stHMI.nEditedType > 0 AND stHMI.nEditedType <= BOTTLE_TYPES_NUMBER THEN
	IF stHMI.nLastEditedType  <> stHMI.nEditedType THEN
		stHMI.stEditedType:=arrTypes[stHMI.nEditedType];
	END_IF
	IF stHMI.bSave THEN
		arrTypes[stHMI.nEditedType]:= stHMI.stEditedType;
		stHMI.bSave:=FALSE;
	END_IF
	IF stHMI.bRestore THEN
		stHMI.stEditedType := arrTypes[stHMI.nEditedType];
		stHMI.bRestore:=FALSE;
	END_IF
	stHMI.nLastEditedType :=   stHMI.nEditedType;
END_IF
//LAMP
IF NOT stInOuts.bI_SafetyEstopOK OR NOT stInOuts.bI_SafetyGateOK THEN
	nLampControl:=RED_BLINK;
ELSIF bError THEN
	nLampControl:=RED;
ELSIF NOT stInOuts.bI_SafetyDrawerOK THEN
	nLampControl:=YELLOW_BLINK;
ELSIF NOT fbTableAxis.prIsHomed THEN
	nLampControl:=YELLOW;
ELSIF bWorkPermission AND iWorkStep=30 THEN
	nLampControl:=GREEN_BLINK;
ELSIF bWorkPermission THEN
	nLampControl:=GREEN;
ELSE 
	nLampControl:=NONE;
END_IF
bBlinkBit := bBlinkBit XOR fbTONBlinkTimer.Q;
fbTONBlinkTimer(IN:= NOT fbTONBlinkTimer.Q,PT:=T#500MS);
stInOuts.bQ_LampBlue:=nLampControl=BLUE OR (nLampControl=BLUE_BLINK AND bBlinkBit);
stInOuts.bQ_LampGreen:=nLampControl=GREEN OR (nLampControl=GREEN_BLINK AND bBlinkBit);
stInOuts.bQ_LampRed:=nLampControl=RED OR (nLampControl=RED_BLINK AND bBlinkBit);
stInOuts.bQ_LampWhite:=nLampControl=WHITE OR (nLampControl=WHITE_BLINK AND bBlinkBit);
stInOuts.bQ_LampYellow:=nLampControl=YELLOW OR (nLampControl=YELLOW_BLINK AND bBlinkBit);
//HMI
stHMI.bEmptyMode R= bAllEmpty;
]]></ST>
    </Implementation>
    <Method Name="mErrorReset" Id="{1efedf66-1b3f-4af7-ad1d-aeb778b44ae6}">
      <Declaration><![CDATA[METHOD mErrorReset : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF iWorkStep =91 THEN
	iWorkStep:=95;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="mGetBottleLoadingParams" Id="{bd99587a-7f8a-4499-9c50-64be08ad39cf}">
      <Declaration><![CDATA[METHOD mGetBottleLoadingParams : ST_LoadingBottleParams
VAR_INPUT
	nBottleType: nBottleTypes;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nBottleType <= BOTTLE_TYPES_NUMBER THEN
	mGetBottleLoadingParams := arrTypes[nBottleType].stLoadingBottleParams;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="mGetBottleUnloadingParams" Id="{cd9e0412-bfb1-4c92-98fc-14f936c33494}">
      <Declaration><![CDATA[METHOD mGetBottleUnloadingParams : ST_UnloadingBottleParams
VAR_INPUT
	nBottleType: nBottleTypes;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nBottleType <= BOTTLE_TYPES_NUMBER THEN
	mGetBottleUnloadingParams := arrTypes[nBottleType].stUnloadingBottleParams;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="mGetBottleWeighingParams" Id="{3598225d-2381-4efe-9653-09691a91f7b1}">
      <Declaration><![CDATA[METHOD mGetBottleWeighingParams : ST_WeighingBottleParams
VAR_INPUT
	nBottleType: nBottleTypes;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nBottleType <= BOTTLE_TYPES_NUMBER THEN
	mGetBottleWeighingParams := arrTypes[nBottleType].stWeighingBottleParams;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="mNewEvent" Id="{ad4822d0-58ff-4947-963c-dba1cc7931e0}">
      <Declaration><![CDATA[METHOD INTERNAL mNewEvent : BOOL
VAR_INPUT
	nEventClass:nMachineEventType;
	nEvent: nTable_ErrorList;
	sMessageString	: STRING(255);
	tMessageTime	: TIME;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nEventClass = nMachineEventType.AlarmAutoConfirm THEN		// Alarm nieblokadowy
	fbPIM.mSetNewMachineState(nMachineStates._0_Initialization);
	iWorkStep := 95;
ELSIF nEventClass = nMachineEventType.Message THEN
	fbPIM.mSetNewEvent(iErrorCode := 0, nEventType := nMachineEventType.Message ,sMessage:=sMessageString,tMessageTime:=tMessageTime);
ELSE
	fbPIM.mSetNewMachineState(nMachineStates._7_ErrorOccurr);
	fbPIM.mSetNewEvent(iErrorCode := UDINT_TO_INT(nEvent), nEventType := nEventClass,sMessage:=sMessageString,tMessageTime:=tMessageTime);
	iWorkStep := 90;
END_IF
nError := 	nEvent;]]></ST>
      </Implementation>
    </Method>
    <Method Name="mNewPart" Id="{f6d917a0-7ecc-494b-8d9b-5a0f1b0ae27f}">
      <Declaration><![CDATA[METHOD mNewPart : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="mPartNOK" Id="{90377635-1d58-405b-bed6-fdd1dc77af60}">
      <Declaration><![CDATA[METHOD mPartNOK : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="mPartOK" Id="{28ba97b0-0863-4915-9262-500e7b59941f}">
      <Declaration><![CDATA[METHOD mPartOK : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="mRequestServoDisable" Id="{c57df235-6686-48e9-bfb3-29ca8f411fbe}">
      <Declaration><![CDATA[METHOD mRequestServoDisable : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[mRequestServoDisable:=fbTableAxis.mRequestDisable();]]></ST>
      </Implementation>
    </Method>
    <Property Name="prNextType" Id="{9b6a4cf6-adf7-4845-b89f-61b3d0260aed}">
      <Declaration><![CDATA[PROPERTY prNextType : nBottleTypes]]></Declaration>
      <Get Name="Get" Id="{560ddfbb-88c9-47ab-b19f-aa6d28e919d4}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prNextType := stHMI.nCurrentTypeToProduce;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prTableInPosition" Id="{1b9542f5-b1b7-456b-b3c1-401cda04a1e0}">
      <Declaration><![CDATA[PROPERTY prTableInPosition : BOOL]]></Declaration>
      <Get Name="Get" Id="{74242f0a-f248-428f-82b1-d392f8f5b487}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prTableInPosition:= fbTableAxis.prPositionValid;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prTableIsMoving" Id="{c158f184-6806-4712-8fe3-97b763dbaee2}">
      <Declaration><![CDATA[PROPERTY prTableIsMoving : BOOL]]></Declaration>
      <Get Name="Get" Id="{aece0678-aed4-4260-99bb-b51f3af0bdd4}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prTableIsMoving:=fbTableAxis.prIsMoving;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prTablePositionNumber" Id="{60756daa-6c95-4115-b5b7-4d2036c9f419}">
      <Declaration><![CDATA[{attribue 'monitoring':= 'call'}
PROPERTY prTablePositionNumber : USINT]]></Declaration>
      <Get Name="Get" Id="{36388db3-ba02-4d7c-9acc-fe593ec419cb}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prTablePositionNumber:=fbTableAxis.prCurrentPositionNumber;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prWorkPermission" Id="{0d7b8f91-6784-4597-9945-b0aada6f376d}">
      <Declaration><![CDATA[{attribue 'monitoring':= 'call'}
PROPERTY prWorkPermission : BOOL]]></Declaration>
      <Get Name="Get" Id="{7ccb89d0-c21b-4fcd-bae2-5e2b448afb3d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prWorkPermission := bWorkPermission;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{78f89997-1120-46b6-917d-3838fcac50d1}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bWorkPermission := prWorkPermission;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <LineIds Name="FB_TableControl">
      <LineId Id="1699" Count="0" />
      <LineId Id="2336" Count="0" />
      <LineId Id="2338" Count="1" />
      <LineId Id="2337" Count="0" />
      <LineId Id="2402" Count="1" />
      <LineId Id="2273" Count="0" />
      <LineId Id="1700" Count="6" />
      <LineId Id="1708" Count="2" />
      <LineId Id="2146" Count="0" />
      <LineId Id="1711" Count="15" />
      <LineId Id="2147" Count="0" />
      <LineId Id="1727" Count="11" />
      <LineId Id="2079" Count="0" />
      <LineId Id="1739" Count="36" />
      <LineId Id="2082" Count="0" />
      <LineId Id="1776" Count="0" />
      <LineId Id="2083" Count="1" />
      <LineId Id="2086" Count="1" />
      <LineId Id="1778" Count="0" />
      <LineId Id="2088" Count="0" />
      <LineId Id="1779" Count="27" />
      <LineId Id="1942" Count="1" />
      <LineId Id="1807" Count="37" />
      <LineId Id="2266" Count="0" />
      <LineId Id="2272" Count="0" />
      <LineId Id="1845" Count="2" />
      <LineId Id="2277" Count="0" />
      <LineId Id="1848" Count="3" />
      <LineId Id="2278" Count="1" />
      <LineId Id="1852" Count="2" />
      <LineId Id="2460" Count="0" />
      <LineId Id="2342" Count="0" />
      <LineId Id="2341" Count="0" />
      <LineId Id="2343" Count="0" />
      <LineId Id="1855" Count="32" />
      <LineId Id="926" Count="0" />
      <LineId Id="2020" Count="1" />
      <LineId Id="2009" Count="1" />
      <LineId Id="2018" Count="1" />
      <LineId Id="2208" Count="0" />
      <LineId Id="2207" Count="0" />
      <LineId Id="2012" Count="1" />
      <LineId Id="2017" Count="0" />
      <LineId Id="2015" Count="0" />
      <LineId Id="2014" Count="0" />
      <LineId Id="2016" Count="0" />
      <LineId Id="2011" Count="0" />
      <LineId Id="2007" Count="1" />
      <LineId Id="1997" Count="0" />
      <LineId Id="2002" Count="4" />
      <LineId Id="2081" Count="0" />
      <LineId Id="2001" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.mErrorReset">
      <LineId Id="5" Count="2" />
    </LineIds>
    <LineIds Name="FB_TableControl.mGetBottleLoadingParams">
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.mGetBottleUnloadingParams">
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.mGetBottleWeighingParams">
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.mNewEvent">
      <LineId Id="16" Count="7" />
      <LineId Id="33" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.mNewPart">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.mPartNOK">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.mPartOK">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.mRequestServoDisable">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.prNextType.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.prTableInPosition.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.prTableIsMoving.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.prTablePositionNumber.Get">
      <LineId Id="2" Count="0" />
      <LineId Id="6" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.prWorkPermission.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TableControl.prWorkPermission.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>