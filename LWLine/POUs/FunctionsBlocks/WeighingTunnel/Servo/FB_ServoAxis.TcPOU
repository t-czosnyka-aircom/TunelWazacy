<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_ServoAxis" Id="{3057f7b8-ac98-4f2d-b555-83dd6c315250}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_ServoAxis ////Blok funkcyjny obslugujacy kompleksowao jedna Os 
VAR_INPUT	
	bSafetyReset			: BOOL;
	bEmergencyStop			: BOOL;	
	bInSafePosition			:BOOL;		
	I_TimeTableRef			: I_TimeTable255;
	sTTRowPreambule			: STRING;
	bELM_ChB				: BOOL;
END_VAR
VAR_OUTPUT
	nActEvent					: nAxis_ErrorList;
	udiActErrorCode				: UDINT;
END_VAR
VAR
	stInOuts					:st_Axis_InOuts;
	fbHomeSensorFTRIG			:F_TRIG;
	iCurrentPositionNumber		:USINT;
	bMoveAbsDone				:BOOL;
	bMoveModuloDone				:BOOL;
	bQuickHomingDone			:BOOL;
	bFullHomingDone				:BOOL;
	bQuickHomingRequest			:BOOL;
	bQuickHomingSynchLost		:BOOL;
	stHMI 						: ST_AxisHMI;
	i:USINT;
	lrPosition:LREAL;
	bCircularPositionsSet:BOOL;
	bMovementInterlock:BOOL;
	bPositionValid :BOOL;
	fbPositionValidTON:TON;
	tPositionValidDelay:TIME:=T#300MS;
	fbHomingSensorTON :TON;
	fbStepTON :TON;
	//OLD
	
	bEnableMotion				: BOOL;		// When TRUE - try to enable axis, when FALSE disable power - safeState
	bStandardSafetyDisable		: BOOL;     // Only safety reset required, No error
	McPower 					: MC_Power;
	McReset 					: MC_Reset;
	McStop	 					: MC_Stop;
	McHalt						: MC_Halt;
	McMoveAbs 					: MC_MoveAbsolute;
	McMoveModulo				: MC_MoveModulo;
	McMoveRel 					: MC_MoveRelative;
	McMoveVelocity	 			: MC_MoveVelocity;
	McJog	 					: MC_Jog;
	McReadActualPosition	 	: MC_ReadActualPosition;
	McReadActualVelocity	 	: MC_ReadActualVelocity;
	McHome						: MC_Home;
	McBrakeCtrl					: FB_BrakeControl;
	McTorqueMonitor				: FB_TorqueMonitoringEL_Ex2;
	McTorqueCtrl				: MC_TorqueControl;
	stHomimgParams				: ST_HomingOptions;
	bLimitSwitchMin: BOOL;
	bLimitSwitchMax: BOOL;
	iWorkStep_Axis: INT;
	fbTON_WorkStepTimeout: TON;
	bSetNewBrakeCtrl: BOOL;
	nAct_BrakeMode, nNew_BrakeMode: nHP_Axis_BrakeMode;
	fbFTRIG_BreakCtrlDone: F_TRIG;
	bTooglePowerState: BOOL;
	astErrorHistory: ARRAY [1..10] OF stErrorHistoryRow;
	fbEnableDelay	:TON;
	
END_VAR
VAR PERSISTENT
	lrReferencePositionAtHomeSensor: LREAL:=-1.0;
	lrLastPositionAtHomeSensor:LREAL:=-1.0;
	stSettings				: ST_Axis_Settings;
END_VAR
VAR CONSTANT
	NUMBER_OF_PRESET_POSITIONS: USINT:=8;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[stInOuts.bQ_SafetyRun :=TRUE;
stInOuts.bQ_SafetyErrorAck:=bSafetyReset;


// Read drive data
stInOuts.stAxis_Data.ReadStatus();
McReadActualPosition(Axis := stInOuts.stAxis_Data, Enable := TRUE);
McReadActualVelocity(Axis := stInOuts.stAxis_Data, Enable := TRUE);

iCurrentPositionNumber := 0;
IF prIsHomed AND prIsNotMoving THEN
	FOR i:=1 TO NUMBER_OF_PRESET_POSITIONS DO
		IF ABS(stSettings.arrPositions[i] - prCurrentPositionModulo) <= stSettings.lrPositionTolerance THEN
			iCurrentPositionNumber := i;
			EXIT;
		ELSIF stSettings.arrPositions[i] -stSettings.lrPositionTolerance <0 THEN
			IF 360.0 + (stSettings.arrPositions[i] -stSettings.lrPositionTolerance) <= prCurrentPositionModulo THEN
				iCurrentPositionNumber := i;
				EXIT;
			END_IF
		END_IF
	END_FOR
END_IF

//Servo position is valid
fbPositionValidTON(IN:= iCurrentPositionNumber <> 0 AND prIsInTargetPosition,PT:=tPositionValidDelay);
bPositionValid:= fbPositionValidTON.Q;

//Read FB status
ACT_AxisFBs(); 	
ACT_AxisWorkSteps();


IF stSettings.bEN_CircularPositions AND NOT bCircularPositionsSet THEN
	lrPosition := 0.0;
	FOR i:=1 TO NUMBER_OF_PRESET_POSITIONS DO
		stSettings.arrPositions[i] := lrPosition;
		lrPosition := lrPosition + 360.0/NUMBER_OF_PRESET_POSITIONS;
	END_FOR
	bCircularPositionsSet:=TRUE;
END_IF


//Quick Homing
fbHomingSensorTON(IN:=stInOuts.bHomingSensor,PT:=T#300MS);
fbHomeSensorFTRIG(CLK:=fbHomingSensorTON.Q);
IF McMoveModulo.Busy AND prIsHomed AND fbHomeSensorFTRIG.Q AND stInOuts.stAxis_Data.Status.PositiveDirection THEN 
	mSavePositionAtHomeSensor();
	IF lrReferencePositionAtHomeSensor = -1.0 THEN
		lrReferencePositionAtHomeSensor := lrLastPositionAtHomeSensor;
	ELSE
		bQuickHomingRequest := stSettings.bEN_QuickHoming;
	END_IF
END_IF

//Manual Mode
IF prIsReady AND bQuickHomingRequest THEN
	mQuickHoming();
ELSIF prIsReady AND stHMI.bManualModeRequest THEN
	iWorkStep_Axis:=15;
END_IF

IF iWorkStep_Axis<>15 THEN
	stHMI.bJogBackwardButton := stHMI.bJogForwardButton := stHMI.bGoToSelectedPosition := FALSE;
END_IF


//HMI
stHMI.bIsReady := prIsReady;
stHMI.bIsHomed := prIsHomed;
stHMI.bIsMoving := prIsMoving;
IF stHMI.bRequestStopAxis  AND_THEN mStopAxis() THEN
	stHMI.bRequestStopAxis := FALSE;
END_IF
]]></ST>
    </Implementation>
    <Folder Name="Methods" Id="{86110dc1-c583-424f-8e1a-53f0e16b101e}" />
    <Folder Name="Properties" Id="{8dc25682-5df1-487c-935d-d480b60343c3}" />
    <Action Name="ACT_AxisFBs" Id="{1733d200-0c8f-4641-80db-f206791f3515}">
      <Implementation>
        <ST><![CDATA[bLimitSwitchMin	:= FALSE;
bLimitSwitchMax := FALSE;
// Power ON
IF stSettings.bEN_HW_LimitSwitches THEN
	IF NOT bELM_ChB THEN
		bLimitSwitchMin	:= NOT stInOuts.wI_DriveInfodata.0;
		bLimitSwitchMax	:= NOT stInOuts.wI_DriveInfodata.1;	
	ELSE
		bLimitSwitchMin	:= NOT stInOuts.wI_DriveInfodata.8;
		bLimitSwitchMax	:= NOT stInOuts.wI_DriveInfodata.9;	
	END_IF
END_IF
McPower(Axis:=stInOuts.stAxis_Data, Enable:=, Enable_Positive:=NOT bLimitSwitchMax, Enable_Negative:= (NOT bLimitSwitchMin OR McHome.Active));

// Error reset
McReset(Axis:=stInOuts.stAxis_Data);

// Homing
McHome(Axis := stInOuts.stAxis_Data, bCalibrationCam := stSettings.bEN_HomingSensor_Invert XOR stInOuts.bHomingSensor, Options := stHomimgParams);

// Stop FBs
McStop(Axis := stInOuts.stAxis_Data, Deceleration := 15000, Jerk := 100000);
McHalt(Axis := stInOuts.stAxis_Data, Deceleration := 15000, Jerk := 100000);
McBrakeCtrl(Axis := stInOuts.stAxis_Data);
fbFTRIG_BreakCtrlDone(CLK:= McBrakeCtrl.Busy);
IF fbFTRIG_BreakCtrlDone.Q THEN
	IF McBrakeCtrl.Mode = eBrakeMode_Automatic THEN	nAct_BrakeMode := nHP_Axis_BrakeMode.BrakeMode_Automatic;
	ELSIF McBrakeCtrl.Mode = eBrakeMode_Lock THEN nAct_BrakeMode := nHP_Axis_BrakeMode.BrakeMode_Lock;
	ELSIF McBrakeCtrl.Mode = eBrakeMode_Unlock THEN nAct_BrakeMode := nHP_Axis_BrakeMode.BrakeMode_Unlock;
	END_IF
END_IF
McBrakeCtrl.Execute := FALSE;
// Motion FBs

McJog(Axis := stInOuts.stAxis_Data, Mode := MC_JOGMODE_CONTINOUS, Acceleration := stSettings.rAcceleration, Deceleration := stSettings.rDeceleration, Jerk := 100000);
McMoveAbs(Axis := stInOuts.stAxis_Data, Acceleration := stSettings.rAcceleration, Deceleration := stSettings.rDeceleration, Jerk := 100000);
McMoveRel(Axis := stInOuts.stAxis_Data, Acceleration := stSettings.rAcceleration, Deceleration := stSettings.rDeceleration, Jerk := 100000);
McMoveVelocity(Axis := stInOuts.stAxis_Data, Acceleration := stSettings.rAcceleration, Deceleration := stSettings.rDeceleration, Jerk := 100000);
McMoveModulo(Axis := stInOuts.stAxis_Data, Acceleration := stSettings.rAcceleration, Deceleration := stSettings.rDeceleration, Jerk := 100000);
// Torque limitation
McTorqueMonitor( bCalculate:= bEnableMotion,
	rTorqueLimit:= ,
	Axis:= stInOuts.stAxis_Data);
McTorqueCtrl(Axis := stInOuts.stAxis_Data, Relative := FALSE, ContinuousUpdate := TRUE, TorqueRamp := 100);]]></ST>
      </Implementation>
    </Action>
    <Action Name="ACT_AxisWorkSteps" Id="{24332e8c-4fda-4b2a-bd2c-6d7b94bd461c}">
      <Implementation>
        <ST><![CDATA[
IF NOT bEnableMotion AND iWorkStep_Axis > 5 AND iWorkStep_Axis < 90 THEN //Safety Stop
	IF bEmergencyStop AND prPositionValid AND bInSafePosition THEN
		iWorkStep_Axis:=90;
		bStandardSafetyDisable := TRUE;
	ELSE
		mNewEvent(nEvent := nAxis_ErrorList.STO_SignalActive, ErrorCode:= 0,bDisable:=TRUE);
	END_IF
	
ELSIF NOT prMovementExternalInterlock AND prIsMoving AND iWorkStep_Axis < 90 AND FALSE  THEN
	mNewEvent(nEvent := nAxis_ErrorList.MovementInterlockLost, ErrorCode:= 0,bDisable:=FALSE);
END_IF
bEnableMotion := stInOuts.bI_SS1_Release AND stInOuts.bI_STO_Release;

stSettings.rSlowSpeed_deg_s := LIMIT(1, stSettings.rSlowSpeed_deg_s, 50);
stSettings.rFastSpeed_deg_s := LIMIT(50, stSettings.rFastSpeed_deg_s, 300);
stSettings.rDeceleration := LIMIT(100, stSettings.rDeceleration, 5000);
stSettings.rAcceleration := LIMIT(100, stSettings.rAcceleration, 5000);

CASE iWorkStep_Axis OF
	0:	// Inicjalizacja
		mCancelAllCommands();
		bStandardSafetyDisable:=FALSE;
		iWorkStep_Axis := 1;
		
	1:	// Sprawdzenie parametrów pracy

		iWorkStep_Axis := 5;
		
	5: 	// Sprawdzenie gotowości do załączenia
		fbEnableDelay(PT:=T#3S,in:=bEnableMotion);
		McPower.Enable := fbEnableDelay.Q;
		IF McPower.Status THEN
			iWorkStep_Axis := 10;
		ELSIF McPower.Error THEN
			mNewEvent( nEvent := nAxis_ErrorList.MotionFB_Error,ErrorCode:= McPower.ErrorID,bDisable:=TRUE);
		END_IF
		
	10:	// Axis ready for new Cmds
		mCancelMoveCommands();
		IF prIsErrorActive THEN
			mNewEvent( nEvent := nAxis_ErrorList.MotionFB_Error,ErrorCode:= stInOuts.stAxis_Data.Status.ErrorID,bDisable:=TRUE);
		END_IF
		fbTON_WorkStepTimeout(IN:= FALSE);
		bMoveAbsDone := bQuickHomingDone:= bMoveModuloDone:= bFullHomingDone:= FALSE;
		
	15:	// Manual mode
		//Jog
		McJog.Velocity := stSettings.rSlowSpeed_deg_s/2;
		McJog(Axis := stInOuts.stAxis_Data, JogBackwards := stHMI.bJogBackwardButton, JogForward := stHMI.bJogForwardButton);
		//Positioning
		McMoveModulo.Direction:= MC_Direction.MC_Shortest_Way;
		McMoveModulo.Position := stSettings.arrPositions[LIMIT(1,stHMI.iManualPositionSelected,NUMBER_OF_PRESET_POSITIONS)];
		McMoveModulo.Velocity := stSettings.rSlowSpeed_deg_s;
		McMoveModulo.Execute:= stHMI.bGoToSelectedPosition;
		McHalt.Execute:=McMoveModulo.Active AND NOT stHMI.bGoToSelectedPosition;
		// Breake control
		IF bSetNewBrakeCtrl THEN
			McBrakeCtrl.Mode := nNew_BrakeMode;
			McBrakeCtrl.Execute := TRUE;	
			bSetNewBrakeCtrl := FALSE; 
		// Enable/disable axis
		ELSIF bTooglePowerState THEN
			McPower.Enable := NOT McPower.Enable;
 			bTooglePowerState := FALSE;
		ELSE
			McPower.Enable S= McJog.JogForward OR McJog.JogBackwards;
		END_IF	

		IF NOT stHMI.bManualModeRequest THEN
			iWorkStep_Axis:=10;
		END_IF
	
		
	20:	// Homing
		McHome.Execute := TRUE;
		IF McHome.Busy THEN
			iWorkStep_Axis := 21;	
		ELSIF McHome.Error THEN
			mNewEvent( nEvent := nAxis_ErrorList.MotionFB_Error,ErrorCode:= McHome.ErrorID,bDisable:=TRUE);
		END_IF
			
	21:	// Home done
		IF McHome.Done THEN
			bQuickHomingSynchLost := FALSE;
			McHome.Execute := FALSE;
			iWorkStep_Axis := 10;
			bFullHomingDone := TRUE;
			//Move to first position
			McMoveModulo.Direction:= MC_Direction.MC_Shortest_Way;
			McMoveModulo.Position := stSettings.arrPositions[1];
			McMoveModulo.Velocity := stSettings.rSlowSpeed_deg_s;
			iWorkStep_Axis := 50;
			lrReferencePositionAtHomeSensor := -1;
		ELSIF McHome.Error THEN
			mNewEvent( nEvent := nAxis_ErrorList.MotionFB_Error,ErrorCode:= McHome.ErrorID,bDisable:=TRUE);
		ELSIF McHome.CommandAborted THEN
			mNewEvent(nEvent := nAxis_ErrorList.CommandAborted, ErrorCode:= McMoveModulo.ErrorID,bDisable:=TRUE);
		END_IF

		
	30:	// Move to absolute position
		McMoveAbs.Execute := TRUE;
		IF McMoveAbs.Busy THEN
			iWorkStep_Axis := 31;	
		ELSIF McMoveAbs.Error THEN
			mNewEvent(nEvent := nAxis_ErrorList.MotionFB_Error, ErrorCode:= McMoveAbs.ErrorID,bDisable:=TRUE);
		END_IF
	
	31:	// Move ABS done
		IF 	McMoveAbs.Done THEN
			McMoveAbs.Execute := FALSE;
			iWorkStep_Axis := 10;	
			bMoveAbsDone := TRUE;
		ELSIF McMoveAbs.Error THEN
			mNewEvent(nEvent := nAxis_ErrorList.MotionFB_Error, ErrorCode:= McMoveAbs.ErrorID,bDisable:=TRUE);
		ELSIF McMoveAbs.CommandAborted THEN	
			mNewEvent(nEvent := nAxis_ErrorList.CommandAborted, ErrorCode:= McMoveModulo.ErrorID,bDisable:=TRUE);
		END_IF
		
		
	40: //Quick Homing
		bQuickHomingRequest:=FALSE;
		McHome.Execute := TRUE;
		IF McHome.Busy THEN
			fbTON_WorkStepTimeout(IN:= FALSE);
			iWorkStep_Axis := 41;
		ELSIF McHome.Error THEN
			mNewEvent( nEvent := nAxis_ErrorList.MotionFB_Error,ErrorCode:= McHome.ErrorID,bDisable:=TRUE);
		END_IF
		
	41: //Quick Homing Done
		IF 	McHome.Done THEN
			iWorkStep_Axis := 10;	
			bQuickHomingDone := TRUE;
			McHome.Execute := FALSE;
		ELSIF McHome.Error THEN
			mNewEvent(nEvent := nAxis_ErrorList.MotionFB_Error, ErrorCode:= McHome.ErrorID,bDisable:=TRUE);
		ELSIF McHome.CommandAborted THEN
			mNewEvent(nEvent := nAxis_ErrorList.CommandAborted, ErrorCode:= McMoveModulo.ErrorID,bDisable:=TRUE);
		END_IF
	
	50:	// Move Modulo
		McMoveModulo.Execute := TRUE;
		IF McMoveModulo.Busy THEN
			iWorkStep_Axis := 51;		// Done
		ELSIF McMoveModulo.Error THEN
			mNewEvent(nEvent := nAxis_ErrorList.MotionFB_Error, ErrorCode:= McMoveModulo.ErrorID,bDisable:=TRUE);
		END_IF
	
	51:	// Move Modulo Done
		IF 	McMoveModulo.Done THEN
			iWorkStep_Axis := 10;	
			bMoveModuloDone := TRUE;
			McMoveModulo.Execute := FALSE;
		ELSIF McMoveModulo.Error THEN
			mNewEvent(nEvent := nAxis_ErrorList.MotionFB_Error, ErrorCode:= McMoveModulo.ErrorID,bDisable:=TRUE);
		ELSIF McMoveModulo.CommandAborted THEN
			mNewEvent(nEvent := nAxis_ErrorList.CommandAborted, ErrorCode:= McMoveModulo.ErrorID,bDisable:=TRUE);
		END_IF
		
	
	90:	// Error
		mCancelAllCommands();
		fbStepTON(IN:=bStandardSafetyDisable, PT:=T#10S); 
		IF bStandardSafetyDisable AND stInOuts.bI_SS1_State  OR NOT bStandardSafetyDisable OR fbStepTON.Q THEN
			iWorkStep_Axis := 91;
			fbStepTON(IN:=FALSE);
		END_IF
		

	91: // Stop 		
		McStop.Execute := TRUE;
		IF McStop.Done OR McStop.Error THEN
			McStop.Execute := FALSE;
			iWorkStep_Axis := 92;
		END_IF
		
	92: // Disable Power 
		McPower.Enable := FALSE;
		IF NOT McPower.Status THEN
			iWorkStep_Axis := 94;
		ELSIF McPower.Error THEN
			mNewEvent(nEvent := nAxis_ErrorList.MotionFB_Error, ErrorCode:= McPower.ErrorID,bDisable:=TRUE);
		END_IF
		
	93: // Stop No Disbale
		mCancelMoveCommands();
		McHalt.Execute := TRUE;
		IF McHalt.Done THEN
			iWorkStep_Axis:=94;
			McHalt.Execute := FALSE;
		ELSIF McHalt.Error THEN
			mNewEvent(nEvent := nAxis_ErrorList.MotionFB_Error, ErrorCode:= McHalt.ErrorID,bDisable:=TRUE);
		END_IF;
		
	94:	// Axis wait for reset
		McReset.Execute := FALSE;
		fbTON_WorkStepTimeout(IN:= FALSE);
		IF bStandardSafetyDisable AND bEnableMotion THEN
			iWorkStep_Axis := 95;
		END_IF
	
	95: // Axis reset
		fbTON_WorkStepTimeout(IN:= TRUE,PT:=T#2S);
		IF stInOuts.stAxis_Data.Status.Error OR prIsErrorActive THEN
			iWorkStep_Axis := 96;
		ELSE
			fbTON_WorkStepTimeout(IN:= FALSE);
			iWorkStep_Axis := 97;
		END_IF
		
	96: // Perform Mc Reset
		McReset.Execute := TRUE;
		IF McReset.Done THEN	// Reset axis
			McReset.Execute := FALSE;
			fbTON_WorkStepTimeout(IN:= FALSE);
			iWorkStep_Axis := 97;
		ELSIF McReset.Error THEN	
			McReset.Execute := FALSE;
			mNewEvent(nEvent := nAxis_ErrorList.MotionFB_Error, ErrorCode:= McReset.ErrorID,bDisable:=TRUE);
		END_IF
		
	97:	// Reset succesfull
		nActEvent := nAxis_ErrorList.NoError;
		udiActErrorCode:= 0;
		fbTON_WorkStepTimeout(IN:= TRUE, PT:= T#40MS);
		IF fbTON_WorkStepTimeout.Q THEN
			fbTON_WorkStepTimeout(IN:= FALSE);
			iWorkStep_Axis := 0;
		END_IF
		
END_CASE

]]></ST>
      </Implementation>
    </Action>
    <Method Name="mAddTimeStat" Id="{ab5ac6de-4b6f-4492-81c5-0cd32a15380e}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mAddTimeStat : BOOL
VAR_INPUT
	sText 		: STRING(255);
END_VAR
VAR
	sTextTmp	: STRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF I_TimeTableRef <> 0 THEN
	CONCAT2(pSrcString1 := ADR(sTTRowPreambule), pSrcString2 := ADR(sText), pDstString := ADR(sTextTmp), nDstSize := SIZEOF(sTextTmp));
	I_TimeTableRef.mPushText(sText := sTextTmp);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="mCancelAllCommands" Id="{db48a037-4fb7-41cd-b68f-6707abdc91b7}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mCancelAllCommands : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[McPower.Enable := McReset.Execute := McHome.Execute := McStop.Execute := McHalt.Execute:=  FALSE;
mCancelMoveCommands();]]></ST>
      </Implementation>
    </Method>
    <Method Name="mCancelMoveCommands" Id="{acbb3440-cf87-41ac-a152-8968e26cb810}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mCancelMoveCommands : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[McJog.JogForward := McJog.JogBackwards := McMoveAbs.Execute := McMoveRel.Execute := McMoveVelocity.Execute := McMoveModulo.Execute:=FALSE; ]]></ST>
      </Implementation>
    </Method>
    <Method Name="mErrorReset" Id="{4c71a6d0-2eb8-4fd5-9ef2-16b78ed57f34}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mErrorReset : BOOL
VAR_OUTPUT
	bError		: BOOL;
	udiErrorId	: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF iWorkStep_Axis = 94 THEN
	iWorkStep_Axis := 95;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="mFullHoming" Id="{a262032b-63e5-4e87-a4b0-e16a24b03a3b}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mFullHoming : BOOL
VAR_INPUT
END_VAR
VAR_OUTPUT
	bBusy		: BOOL;
	bError		: BOOL;
	udiErrorId	: UDINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[mFullHoming := FALSE;
IF prIsReady THEN
	iWorkStep_Axis := 20;
	McHome.Position := 357;
	McHome. HomingMode := MC_HomingMode.MC_DefaultHoming;
	stHomimgParams.SearchDirection := MC_Direction.MC_Positive_Direction;
	stHomimgParams.SyncDirection := MC_Direction.MC_Negative_Direction;
	stHomimgParams.SearchVelocity := 10.0;
	stHomimgParams.SyncVelocity := 1;
	stHomimgParams.ReferenceMode := E_EncoderReferenceMode.ENCODERREFERENCEMODE_DEFAULt;
ELSIF McHome.Error OR iWorkStep_Axis < 20 OR iWorkStep_Axis > 21 THEN
	mFullHoming := TRUE;
	bError := TRUE;
	udiErrorId := INT_TO_UINT(iWorkStep_Axis);	
END_IF

bBusy := McHome.Busy;
IF bFullHomingDone THEN
	mFullHoming := TRUE;
	bFullHomingDone :=FALSE;
END_IF;
udiErrorId := McHome.ErrorID;]]></ST>
      </Implementation>
    </Method>
    <Method Name="mMoveFastToPresetPos" Id="{17b7a812-32ab-4488-9109-06aa0299f113}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mMoveFastToPresetPos : BOOL
VAR_INPUT
	iPosNumber	:USINT;
	bFast:BOOL;
END_VAR
VAR_OUTPUT
	bBusy		: BOOL;
	bError		: BOOL;
	udiErrorId	: UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[mMoveFastToPresetPos :=FALSE;
IF prIsReady AND prIsHomed THEN
	iPosNumber := MIN(20,iPosNumber);
	McMoveAbs.Position := stSettings.arrPositions[iPosNumber];
	IF bFast THEN
		McMoveAbs.Velocity := stSettings.rFastSpeed_deg_s;
	ELSE
		McMoveAbs.Velocity := stSettings.rSlowSpeed_deg_s;
	END_IF
	iWorkStep_Axis := 30;
ELSIF McMoveAbs.Error OR iWorkStep_Axis <30 OR iWorkStep_Axis >31  THEN
	mMoveFastToPresetPos := TRUE;
	bError := TRUE;
	udiErrorId := INT_TO_UINT(iWorkStep_Axis);	
END_IF

bBusy := McMoveAbs.Busy;
IF bMoveAbsDone THEN
	mMoveFastToPresetPos := TRUE;
	bMoveAbsDone :=FALSE;
END_IF;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="mMoveModuloToPosition" Id="{b9786ee7-83c2-4602-a755-a5d8da1d53c5}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mMoveModuloToPosition : BOOL
VAR_INPUT
	iPosNumber	:USINT;
	bFast:BOOL;
END_VAR
VAR_OUTPUT
	bBusy		: BOOL;
	bError		: BOOL;
	bNotReady	: BOOL;
	udiErrorId	: UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[mMoveModuloToPosition :=FALSE;
IF prIsReady AND prIsHomed THEN
	McMoveModulo.Direction:= MC_Direction.MC_Shortest_Way;
	iPosNumber := MIN(20,iPosNumber);
	McMoveModulo.Position := stSettings.arrPositions[iPosNumber];
	IF bFast THEN
		McMoveModulo.Velocity := stSettings.rFastSpeed_deg_s;
	ELSE
		McMoveModulo.Velocity := stSettings.rSlowSpeed_deg_s;
	END_IF
	iWorkStep_Axis := 50;	
ELSIF McMoveModulo.Error THEN
	mMoveModuloToPosition := TRUE;
	bError := TRUE;
	udiErrorId :=McMoveModulo.ErrorID;	
ELSIF iWorkStep_Axis <50 OR iWorkStep_Axis >51 THEN
	mMoveModuloToPosition := TRUE;
	bNotReady := TRUE;
	udiErrorId := INT_TO_UINT(iWorkStep_Axis);	
END_IF

bBusy := McMoveModulo.Busy;
IF bMoveModuloDone THEN
	mMoveModuloToPosition := TRUE;
	bMoveModuloDone :=FALSE;
END_IF;

]]></ST>
      </Implementation>
    </Method>
    <Method Name="mNewEvent" Id="{45dc65cc-cc78-49f4-a976-6b04a137582d}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD INTERNAL mNewEvent : BOOL
VAR_INPUT
	nEvent: nAxis_ErrorList;
	ErrorCode: UDINT;
	bDisable: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bStandardSafetyDisable :=FALSE;
MEMMOVE(srcAddr:= ADR(astErrorHistory[1]), destAddr := ADR(astErrorHistory[2]), n:= 9* SIZEOF(astErrorHistory[1]));
astErrorHistory[1].tTimeStamp := TIME();
astErrorHistory[1].sDescription := TO_STRING(nEvent);
astErrorHistory[1].iWorkStep1 := iWorkStep_Axis;
astErrorHistory[1].udiErrorCode := ErrorCode;
IF bDisable THEN
	iWorkStep_Axis := 90;
ELSE
	iWorkStep_Axis := 93;
END_IF
nActEvent := nEvent;
udiActErrorCode:= ErrorCode;


]]></ST>
      </Implementation>
    </Method>
    <Method Name="mQuickHoming" Id="{5b03c9ef-9972-4bbe-81ff-bccad90f604c}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mQuickHoming : BOOL
VAR_INPUT
END_VAR
VAR_OUTPUT
	bBusy		: BOOL;
	bError		: BOOL;
	udiErrorId	: UDINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[mQuickHoming :=FALSE;
IF prIsReady AND lrReferencePositionAtHomeSensor > 0 AND lrLastPositionAtHomeSensor > 0 THEN
	IF ABS(lrReferencePositionAtHomeSensor - lrLastPositionAtHomeSensor) > 1.0 THEN
		mNewEvent( nEvent := nAxis_ErrorList.QuickHomingError,ErrorCode:= McHome.ErrorID,bDisable:=FALSE);
		bQuickHomingSynchLost :=TRUE;
		bQuickHomingRequest:=FALSE;
	ELSE
		McHome.Position := prCurrentPositionModulo + (lrReferencePositionAtHomeSensor - lrLastPositionAtHomeSensor);
		McHome. HomingMode := MC_HomingMode.MC_Direct;
		iWorkStep_Axis := 40;
	END_IF
ELSIF McHome.Error OR iWorkStep_Axis <40 OR iWorkStep_Axis >41  THEN
	mQuickHoming := TRUE;
	bError := TRUE;
	udiErrorId := INT_TO_UINT(iWorkStep_Axis);	
END_IF

bBusy := McHome.Busy;
IF bQuickHomingDone THEN
	mQuickHoming := TRUE;
	bQuickHomingDone :=FALSE;
END_IF;
udiErrorId := McHome.ErrorID;]]></ST>
      </Implementation>
    </Method>
    <Method Name="mSavePositionAtHomeSensor" Id="{3a59daed-c1be-449e-8865-4c1bfb4a7379}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mSavePositionAtHomeSensor : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF prCurrentPositionModulo <> -1 THEN
	lrLastPositionAtHomeSensor:=prCurrentPositionModulo;
END_IF;]]></ST>
      </Implementation>
    </Method>
    <Method Name="mStopAxis" Id="{09d463fc-b21d-48e3-8ec5-8c22313c676f}" FolderPath="Methods\">
      <Declaration><![CDATA[METHOD mStopAxis : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[mNewEvent(nEvent := nAxis_ErrorList.CommandAborted, ErrorCode:= 0,bDisable:=FALSE);
mStopAxis:=TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="prActualPosition" Id="{f40194ba-8306-4aaa-b5b5-5fb0758d9b45}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prActualPosition : LREAL]]></Declaration>
      <Get Name="Get" Id="{8bac339c-d060-4766-ad8b-fd8c908a3488}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF McReadActualPosition.Valid AND prIsHomed THEN		
	prActualPosition := McReadActualPosition.Position;
ELSE
	prActualPosition := -1;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prActualVelocity" Id="{ff223880-cd07-4f47-8533-6780d3523a7c}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prActualVelocity : LREAL]]></Declaration>
      <Get Name="Get" Id="{78818ba6-1efe-447e-8266-54cd48988813}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF McReadActualVelocity.Valid THEN
	prActualVelocity := McReadActualVelocity.ActualVelocity;
ELSE
	prActualVelocity := -1;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prCurrentPositionModulo" Id="{a7f3473b-83b3-4341-bc35-77febc0fdef3}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prCurrentPositionModulo : LREAL]]></Declaration>
      <Get Name="Get" Id="{79826043-5518-4c9c-8b03-0cb607eb68d1}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF McReadActualPosition.Valid AND prIsHomed THEN		
	prCurrentPositionModulo := stInOuts.stAxis_Data.NcToPlc.ModuloActPos;
ELSE
	prCurrentPositionModulo := -1;
END_IF]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prCurrentPositionNumber" Id="{ea3d6f95-12cc-4ac1-9e24-d1e623d6380c}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prCurrentPositionNumber : USINT]]></Declaration>
      <Get Name="Get" Id="{5c223598-c8c8-44d8-8e1f-7aaddfe49b9a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prCurrentPositionNumber:=iCurrentPositionNumber;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prEnabled" Id="{20ee9f61-720c-40a2-84c5-331a0b62253a}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prEnabled : BOOL]]></Declaration>
      <Get Name="Get" Id="{6f777018-1d15-4443-86fe-bcc9014295bc}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prEnabled:= McPower.Status;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prIsErrorActive" Id="{da3f3089-581e-41ca-8bcd-c78e51817caa}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prIsErrorActive : BOOL]]></Declaration>
      <Get Name="Get" Id="{436b39d5-59d0-48e6-8f59-40a1bc78273a}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prIsErrorActive := stInOuts.stAxis_Data.Status.Error OR (iWorkStep_Axis >= 90 AND NOT bStandardSafetyDisable);]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prIsHomed" Id="{f71d9df2-615f-49d5-9f8f-a7959eb3e890}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prIsHomed : BOOL]]></Declaration>
      <Get Name="Get" Id="{214f06e1-9c57-412c-bce6-1826f7c03c7d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prIsHomed := stInOuts.stAxis_Data.Status.Homed AND NOT bQuickHomingSynchLost;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prIsInTargetPosition" Id="{7c58df86-0d69-4a2c-830b-ded4fd793ede}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prIsInTargetPosition : BOOL]]></Declaration>
      <Get Name="Get" Id="{e786766e-c47f-4a16-8b97-2ac914454196}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prIsInTargetPosition := stInOuts.stAxis_Data.Status.InTargetPosition;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prIsMoving" Id="{d921ac5b-ba42-4853-9806-c4dcb58d889c}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prIsMoving : BOOL]]></Declaration>
      <Get Name="Get" Id="{f68ebf89-02ea-47cb-8cf6-53ef2c6e6de6}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prIsMoving := stInOuts.stAxis_Data.Status.Moving;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prIsNotMoving" Id="{55c0575a-37a0-40ed-985f-602114fa2c8e}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prIsNotMoving : BOOL]]></Declaration>
      <Get Name="Get" Id="{cb1572cd-ba6b-409d-8282-8e38c003a699}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prIsNotMoving :=  stInOuts.stAxis_Data.Status.NotMoving;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prIsReady" Id="{6e65633e-98e6-41e4-9524-0f21768d5334}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prIsReady : BOOL]]></Declaration>
      <Get Name="Get" Id="{4e0e5a66-9481-4905-b577-efae5ddc6ef5}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prIsReady := stInOuts.stAxis_Data.Status.Operational AND NOT stInOuts.stAxis_Data.Status.Error AND McPower.Status AND iWorkStep_Axis = 10 AND prMovementExternalInterlock;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prMovementExternalInterlock" Id="{3b41f782-19d6-4140-8d2f-1b560d59e4d3}" FolderPath="Properties\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY prMovementExternalInterlock : BOOL]]></Declaration>
      <Get Name="Get" Id="{48dd8a24-9dee-4b4f-bea6-609d20afc7b2}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prMovementExternalInterlock:=bMovementInterlock;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{ba5d0f7e-ab05-44f9-94f9-be69fe117f78}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[bMovementInterlock:=prMovementExternalInterlock;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="prPositionValid" Id="{8bd3caab-2706-4652-9796-d1cef13b5dc9}" FolderPath="Properties\">
      <Declaration><![CDATA[PROPERTY prPositionValid : BOOL]]></Declaration>
      <Get Name="Get" Id="{de089de8-ae53-46f8-95a8-55578f05c046}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prPositionValid:=bPositionValid;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <LineIds Name="FB_ServoAxis">
      <LineId Id="1093" Count="1" />
      <LineId Id="1243" Count="0" />
      <LineId Id="1172" Count="0" />
      <LineId Id="953" Count="9" />
      <LineId Id="1319" Count="0" />
      <LineId Id="1314" Count="1" />
      <LineId Id="1317" Count="0" />
      <LineId Id="1321" Count="0" />
      <LineId Id="1318" Count="0" />
      <LineId Id="1316" Count="0" />
      <LineId Id="965" Count="9" />
      <LineId Id="977" Count="9" />
      <LineId Id="1393" Count="1" />
      <LineId Id="987" Count="0" />
      <LineId Id="1395" Count="0" />
      <LineId Id="988" Count="0" />
      <LineId Id="1396" Count="5" />
      <LineId Id="1403" Count="0" />
      <LineId Id="996" Count="20" />
      <LineId Id="402" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.ACT_AxisFBs">
      <LineId Id="2" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="3" Count="0" />
      <LineId Id="43" Count="0" />
      <LineId Id="4" Count="6" />
      <LineId Id="44" Count="0" />
      <LineId Id="11" Count="9" />
      <LineId Id="41" Count="0" />
      <LineId Id="21" Count="19" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.ACT_AxisWorkSteps">
      <LineId Id="9" Count="1" />
      <LineId Id="440" Count="1" />
      <LineId Id="446" Count="0" />
      <LineId Id="443" Count="1" />
      <LineId Id="442" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="375" Count="1" />
      <LineId Id="12" Count="0" />
      <LineId Id="377" Count="0" />
      <LineId Id="486" Count="0" />
      <LineId Id="483" Count="2" />
      <LineId Id="482" Count="0" />
      <LineId Id="13" Count="2" />
      <LineId Id="374" Count="0" />
      <LineId Id="452" Count="0" />
      <LineId Id="18" Count="2" />
      <LineId Id="24" Count="0" />
      <LineId Id="26" Count="2" />
      <LineId Id="453" Count="0" />
      <LineId Id="438" Count="0" />
      <LineId Id="33" Count="4" />
      <LineId Id="44" Count="1" />
      <LineId Id="383" Count="0" />
      <LineId Id="46" Count="3" />
      <LineId Id="291" Count="0" />
      <LineId Id="51" Count="1" />
      <LineId Id="358" Count="0" />
      <LineId Id="356" Count="1" />
      <LineId Id="60" Count="0" />
      <LineId Id="378" Count="0" />
      <LineId Id="361" Count="0" />
      <LineId Id="367" Count="0" />
      <LineId Id="366" Count="0" />
      <LineId Id="365" Count="0" />
      <LineId Id="61" Count="11" />
      <LineId Id="351" Count="0" />
      <LineId Id="348" Count="2" />
      <LineId Id="73" Count="0" />
      <LineId Id="354" Count="0" />
      <LineId Id="74" Count="0" />
      <LineId Id="368" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="78" Count="2" />
      <LineId Id="91" Count="2" />
      <LineId Id="95" Count="0" />
      <LineId Id="480" Count="0" />
      <LineId Id="96" Count="1" />
      <LineId Id="373" Count="0" />
      <LineId Id="466" Count="0" />
      <LineId Id="481" Count="0" />
      <LineId Id="463" Count="2" />
      <LineId Id="489" Count="0" />
      <LineId Id="371" Count="1" />
      <LineId Id="391" Count="1" />
      <LineId Id="390" Count="0" />
      <LineId Id="98" Count="2" />
      <LineId Id="283" Count="0" />
      <LineId Id="101" Count="0" />
      <LineId Id="103" Count="2" />
      <LineId Id="284" Count="0" />
      <LineId Id="112" Count="1" />
      <LineId Id="118" Count="0" />
      <LineId Id="380" Count="0" />
      <LineId Id="119" Count="0" />
      <LineId Id="288" Count="2" />
      <LineId Id="388" Count="1" />
      <LineId Id="287" Count="0" />
      <LineId Id="189" Count="0" />
      <LineId Id="292" Count="1" />
      <LineId Id="347" Count="0" />
      <LineId Id="319" Count="5" />
      <LineId Id="327" Count="0" />
      <LineId Id="336" Count="4" />
      <LineId Id="381" Count="0" />
      <LineId Id="341" Count="1" />
      <LineId Id="386" Count="1" />
      <LineId Id="294" Count="1" />
      <LineId Id="302" Count="11" />
      <LineId Id="382" Count="0" />
      <LineId Id="314" Count="1" />
      <LineId Id="384" Count="1" />
      <LineId Id="316" Count="0" />
      <LineId Id="296" Count="1" />
      <LineId Id="190" Count="1" />
      <LineId Id="487" Count="0" />
      <LineId Id="456" Count="0" />
      <LineId Id="459" Count="0" />
      <LineId Id="488" Count="0" />
      <LineId Id="462" Count="0" />
      <LineId Id="457" Count="0" />
      <LineId Id="196" Count="0" />
      <LineId Id="425" Count="0" />
      <LineId Id="427" Count="1" />
      <LineId Id="439" Count="0" />
      <LineId Id="429" Count="1" />
      <LineId Id="426" Count="0" />
      <LineId Id="419" Count="2" />
      <LineId Id="433" Count="0" />
      <LineId Id="436" Count="1" />
      <LineId Id="423" Count="0" />
      <LineId Id="417" Count="0" />
      <LineId Id="395" Count="3" />
      <LineId Id="394" Count="0" />
      <LineId Id="400" Count="1" />
      <LineId Id="403" Count="0" />
      <LineId Id="399" Count="0" />
      <LineId Id="240" Count="3" />
      <LineId Id="448" Count="2" />
      <LineId Id="244" Count="1" />
      <LineId Id="249" Count="0" />
      <LineId Id="262" Count="5" />
      <LineId Id="404" Count="9" />
      <LineId Id="269" Count="0" />
      <LineId Id="414" Count="0" />
      <LineId Id="270" Count="1" />
      <LineId Id="379" Count="0" />
      <LineId Id="272" Count="5" />
      <LineId Id="281" Count="0" />
      <LineId Id="355" Count="0" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mAddTimeStat">
      <LineId Id="3" Count="2" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mCancelAllCommands">
      <LineId Id="5" Count="1" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mCancelMoveCommands">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mErrorReset">
      <LineId Id="29" Count="1" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mFullHoming">
      <LineId Id="45" Count="0" />
      <LineId Id="32" Count="1" />
      <LineId Id="56" Count="5" />
      <LineId Id="55" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="37" Count="2" />
      <LineId Id="44" Count="0" />
      <LineId Id="47" Count="5" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mMoveFastToPresetPos">
      <LineId Id="102" Count="21" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mMoveModuloToPosition">
      <LineId Id="62" Count="0" />
      <LineId Id="3" Count="0" />
      <LineId Id="89" Count="0" />
      <LineId Id="43" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="67" Count="2" />
      <LineId Id="18" Count="0" />
      <LineId Id="55" Count="3" />
      <LineId Id="100" Count="0" />
      <LineId Id="102" Count="1" />
      <LineId Id="101" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="63" Count="1" />
      <LineId Id="28" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mNewEvent">
      <LineId Id="3" Count="4" />
      <LineId Id="9" Count="2" />
      <LineId Id="26" Count="2" />
      <LineId Id="14" Count="2" />
      <LineId Id="19" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mQuickHoming">
      <LineId Id="9" Count="1" />
      <LineId Id="49" Count="1" />
      <LineId Id="53" Count="1" />
      <LineId Id="51" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="31" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="20" Count="10" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mSavePositionAtHomeSensor">
      <LineId Id="6" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="7" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.mStopAxis">
      <LineId Id="18" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prActualPosition.Get">
      <LineId Id="3" Count="3" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prActualVelocity.Get">
      <LineId Id="3" Count="3" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prCurrentPositionModulo.Get">
      <LineId Id="5" Count="3" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prCurrentPositionNumber.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prEnabled.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prIsErrorActive.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prIsHomed.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prIsInTargetPosition.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prIsMoving.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prIsNotMoving.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prIsReady.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prMovementExternalInterlock.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prMovementExternalInterlock.Set">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_ServoAxis.prPositionValid.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>