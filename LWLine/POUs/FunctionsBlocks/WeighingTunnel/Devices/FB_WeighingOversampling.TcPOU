<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="FB_WeighingOversampling" Id="{3519dee1-614f-442c-be8a-15af5c35d0c3}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_WeighingOversampling
VAR
	arrSamples AT %I* : ARRAY[1..OVERSAMPLING_FACTOR] OF DINT;
	arrBuffer : ARRAY[1..MAX_BUFFER_SIZE] OF REAL;
	rCycleAvg : LREAL;
	rAccumulate: LREAL;
	i	:INT;
	fbLoadFiler 		: FB_FilterEx;
	index :INT:=1;
	rRunningAcc	: LREAL;
	bFilled : BOOL;
	rRunningMeasure : LREAL;
	rRunningMeasureWithTare : LREAL;
	rTest:REAL;
	iCycles: UINT;
	iReqCycles :UINT;
	bGetMeasurement:BOOL;
	bMeasurementReady: BOOL;;
	rSingleMess: REAL;
	rSingleMessResult: REAL;
	fbRunningAnalog : fbAnalogMeasurement;
	fbSingleAnalog  : fbAnalogMeasurement;
	
	bReqMeasurement : BOOL;
	fbReqMeasurement :R_TRIG;

	fbTON_ForceSampling	: TON;
	fbTON_ForceStable	: TON;
	rLastRunningMeasure: REAL;
	bMeasurementError : BOOL;
	fbSingleMeasurementTimeout: TON;
	rSingleMeasurmentRes:REAL;
	rRunningMeasurmentRes:REAL;
END_VAR
VAR PERSISTENT
	stSettings:  ST_WeighingSettings;
	rZeroValue 		: REAL;
END_VAR
VAR CONSTANT
	OVERSAMPLING_FACTOR: INT:=1;
	MAX_BUFFER_SIZE:INT:=1000;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[
rAccumulate := 0;
FOR i := 1 TO OVERSAMPLING_FACTOR DO
    rAccumulate := rAccumulate + arrSamples[i];
END_FOR
rCycleAvg := rAccumulate/OVERSAMPLING_FACTOR ;

//Running Measurment
rRunningAcc := rRunningAcc - arrBuffer[index];
rRunningAcc := rRunningAcc + rCycleAvg;
arrBuffer[index] := rCycleAvg ;
index := index + 1;
IF index > MAX_BUFFER_SIZE THEN
	index :=1;
	bFilled := TRUE;
END_IF
IF bFilled THEN
	rRunningMeasure := rRunningAcc/MAX_BUFFER_SIZE;
END_IF

fbTON_ForceSampling(IN:= NOT fbTON_ForceSampling.Q, PT:= T#30MS);
IF fbTON_ForceSampling.Q THEN
	fbTON_ForceStable(IN:= ABS(rLastRunningMeasure-rRunningMeasure)< MAX(0.5,rRunningMeasure*0.005), PT:= INT_TO_TIME(stSettings.iWeightStabilisationTimeMS));
	rLastRunningMeasure := rRunningMeasure;
END_IF



//Single Measurment
fbSingleMeasurementTimeout(IN:=bGetMeasurement AND NOT prForceIsStable AND iCycles = 0,PT:= T#5S);

fbReqMeasurement(CLK:=bReqMeasurement);
IF fbReqMeasurement.Q THEN
	bGetMeasurement := TRUE;
	bMeasurementError := FALSE;
	bMeasurementReady := FALSE;
END_IF
IF bGetMeasurement AND prForceIsStable THEN
	rSingleMess := rSingleMess + rCycleAvg;
	iCycles := iCycles +1;
	IF iCycles >= iReqCycles THEN
		rSingleMessResult := rSingleMess/iCycles;
		bMeasurementReady := TRUE;
		bGetMeasurement:= FALSE;
		iCycles :=0;
		rSingleMess := 0.0;
	END_IF
ELSIF bGetMeasurement AND NOT prForceIsStable AND iCycles > 0 OR fbSingleMeasurementTimeout.Q THEN
	bMeasurementError := TRUE;
	bGetMeasurement:= FALSE;
	iCycles :=0;
	rSingleMess := 0.0;
END_IF

//Scalling
fbSingleAnalog(rIn:=rSingleMessResult,stMeasureScalingData:=stSettings.stScalling, tStalaCzasowa:=T#1MS);
fbRunningAnalog(rIn:=rRunningMeasure,stMeasureScalingData:=stSettings.stScalling, tStalaCzasowa:=T#1MS);
rSingleMeasurmentRes := fbSingleAnalog.rOutFiltered - rZeroValue;
rRunningMeasurmentRes := fbRunningAnalog.rOutFiltered - rZeroValue;
//HMI
IF stSettings.bManualSingleMeasureRequest AND_THEN mGetMeasurement(1000) THEN
	stSettings.bManualSingleMeasureRequest:=FALSE;
END_IF
IF stSettings.bTareRequest AND_THEN mTare() THEN
	stSettings.bTareRequest:=FALSE;
END_IF

]]></ST>
    </Implementation>
    <Method Name="mGetMeasurement" Id="{b755d138-d4ca-4b3a-9962-72c9a6f2bf0b}">
      <Declaration><![CDATA[METHOD mGetMeasurement : BOOL
VAR_INPUT
	iCycles: INT;
END_VAR
VAR
	bInit:BOOL:=FALSE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
bReqMeasurement := TRUE;
iReqCycles := iCycles;
mGetMeasurement := FALSE;
IF bMeasurementReady OR bMeasurementError THEN
	mGetMeasurement := TRUE;
	bMeasurementReady := bReqMeasurement := FALSE;
END_IF;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mTare" Id="{9b6dea7e-18b9-466a-aba8-9e503c4931be}">
      <Declaration><![CDATA[METHOD mTare : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[mTare:=FALSE;
rZeroValue := 0;
IF mGetMeasurement(1000) THEN
	IF NOT prSingleMeasurementError THEN
		rZeroValue := prSingleMeasurement;
	END_IF
	mTare:=TRUE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="prForceIsStable" Id="{6c1dbff3-59e6-4566-9180-c7daab8a55bf}">
      <Declaration><![CDATA[{attribute 'monitoring':= 'call'}
PROPERTY prForceIsStable : BOOL]]></Declaration>
      <Get Name="Get" Id="{4fb681c2-9cf1-4ee4-9515-20464fe7afda}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prForceIsStable := fbTON_ForceStable.Q;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prMeasurmentInProgress" Id="{9f304df3-828c-4116-bc1f-253bc0b1da5d}">
      <Declaration><![CDATA[PROPERTY prMeasurmentInProgress : BOOL]]></Declaration>
      <Get Name="Get" Id="{a3a2bf40-9ca0-4f7d-a6ce-1c71aa8f6a3c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prMeasurmentInProgress := bReqMeasurement;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prRunningMeasurement" Id="{bcc25dcf-27df-4f0a-858c-49c915207399}">
      <Declaration><![CDATA[{attribute 'monitoring':= 'call'}
PROPERTY prRunningMeasurement : REAL]]></Declaration>
      <Get Name="Get" Id="{1221aa39-6940-4645-9480-b2c10f3a9d64}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prRunningMeasurement := rRunningMeasurmentRes;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prSingleMeasurement" Id="{dfd4f5d1-d18d-4698-9a97-7bd6b9c12f74}">
      <Declaration><![CDATA[{attribute 'monitoring':= 'call'}
PROPERTY prSingleMeasurement : REAL]]></Declaration>
      <Get Name="Get" Id="{253c9750-b34b-4117-8494-502b0d5e4059}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prSingleMeasurement := rSingleMeasurmentRes;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prSingleMeasurementError" Id="{be6856fd-b8eb-4733-9767-175d2464b50a}">
      <Declaration><![CDATA[{attribute 'monitoring':= 'call'}
PROPERTY prSingleMeasurementError : BOOL]]></Declaration>
      <Get Name="Get" Id="{49bc2472-d6e4-4733-8e2d-f367c54a123c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prSingleMeasurementError := bMeasurementError;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="prSingleMeasurementRaw" Id="{56186388-389d-4ad3-a899-5843ef86ec71}">
      <Declaration><![CDATA[{attribute 'monitoring':= 'call'}
PROPERTY prSingleMeasurementRaw : REAL]]></Declaration>
      <Get Name="Get" Id="{f9136aac-b5d3-4c8f-98bc-6e528a51fe72}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[prSingleMeasurementRaw := rSingleMessResult;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <LineIds Name="FB_WeighingOversampling">
      <LineId Id="619" Count="55" />
      <LineId Id="126" Count="0" />
      <LineId Id="681" Count="0" />
      <LineId Id="733" Count="0" />
      <LineId Id="680" Count="0" />
      <LineId Id="683" Count="2" />
      <LineId Id="687" Count="0" />
      <LineId Id="690" Count="0" />
      <LineId Id="689" Count="0" />
      <LineId Id="686" Count="0" />
      <LineId Id="682" Count="0" />
    </LineIds>
    <LineIds Name="FB_WeighingOversampling.mGetMeasurement">
      <LineId Id="33" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="17" Count="0" />
    </LineIds>
    <LineIds Name="FB_WeighingOversampling.mTare">
      <LineId Id="13" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="11" Count="0" />
    </LineIds>
    <LineIds Name="FB_WeighingOversampling.prForceIsStable.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_WeighingOversampling.prMeasurmentInProgress.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_WeighingOversampling.prRunningMeasurement.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_WeighingOversampling.prSingleMeasurement.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_WeighingOversampling.prSingleMeasurementError.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_WeighingOversampling.prSingleMeasurementRaw.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>